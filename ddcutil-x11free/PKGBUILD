# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil-x11free
pkgver=1.3.0
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=('x86_64')
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm')
makedepends=('systemd')
provides=('ddcutil')
conflicts=('ddcutil')
source=(https://github.com/rockowitz/ddcutil/archive/v$pkgver/ddcutil-$pkgver.tar.gz)
sha512sums=('93d69958b2e1dcb66ad700712742536179383c93f2ec1f746b6a6c78adfd6daa861275208850cc807e0c16a11ea84904cb13ceef512ca76e1daa910410303d41')

prepare() {
  cd ddcutil-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ddcutil-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-x11 \

  make
}

package() {
  cd ddcutil-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
