# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=podman-compose
pkgname=$_pkgname-uosys
pkgver=0.1.5.r99.g039fe30
_commit=039fe30a7096e622ddbc109778d9e60a8c8ba6b0
pkgrel=1
pkgdesc='A script to run docker-compose.yml using podman'
arch=('any')
url='https://github.com/containers/podman-compose'
license=('GPL2')
backup=('etc/conf.d/podman-compose')
depends=('python' 'python-yaml' 'podman')
makedepends=('git' 'python-setuptools')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("git+$url#commit=$_commit"
        'podman-compose.conf.d'
        'podman-compose.conf.d.user'
        'podman-compose@.service'
        'podman-compose@.service.user'
        'tmpfiles.d.conf')
sha256sums=('SKIP'
            'c7da97c64b8f7f592b1be6f932c8106a71bbfaaae433f085f3f269fa26d4777a'
            '2da066ddf626a4d60ad44e044837b9316382ffba263c42c117ce03de3f4451dc'
            '0ef8c1fcc440db3953818e2cc7e20fcfaffdc2f06c4a0651d3874259a844327f'
            '661af0aa83a5f6056e44d355b30807209aa641b58ee07dfb0f836e35e3b4b650'
            '1bfdcb90ba5d93f1db2ad77561e9f3b57522528083283c3aabc2aa8ff2ee551d')

pkgver() {
  cd $_pkgname
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 ../podman-compose@.service "$pkgdir"/usr/lib/systemd/system/podman-compose@.service
  install -Dm644 ../podman-compose.conf.d "$pkgdir"/etc/conf.d/podman-compose
  install -Dm644 ../podman-compose@.service.user "$pkgdir"/usr/lib/systemd/user/podman-compose@.service
  install -Dm644 ../podman-compose.conf.d.user "$pkgdir"/usr/doc/podman-compose/user/podman-compose.conf.d

  # uosys-mods
  #install -dm755 "$pkgdir"/etc/podman/compose
  install -dm755 "$pkgdir"/etc/podman
  ln -s /var/etc/podman/compose "$pkgdir"/etc/podman/compose
  install -Dm644 ../tmpfiles.d.conf "$pkgdir"/usr/lib/tmpfiles.d/uosys-mods-podman-compose.conf
}

# vim:set ts=2 sw=2 et:
