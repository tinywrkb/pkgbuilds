# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

# WARNING: this doesn't build, bluez midi code depends on alsa-lib only api

_pkgbase=bluez
_pkgpostfix=tinyalsa
pkgbase=$_pkgbase-$_pkgpostfix
pkgname=(bluez{,-utils,-libs,-cups,-hid2hci,-plugins}-$_pkgpostfix)
pkgver=5.60
pkgrel=1
url="http://www.bluez.org/"
arch=('x86_64')
license=('GPL2')
makedepends=('dbus' 'libical' 'systemd' 'tinyalsa' 'json-c' 'ell' 'python-docutils')
source=(https://www.kernel.org/pub/linux/bluetooth/bluez-${pkgver}.tar.{xz,sign}
        bluetooth.modprobe
)
# see https://www.kernel.org/pub/linux/bluetooth/sha256sums.asc
sha256sums=('710999580d01ee59ec585e5e7c07fd94eddedc001aa26fe7464c546f9d945304'
            'SKIP'
            '46c021be659c9a1c4e55afd04df0c059af1f3d98a96338236412e449bf7477b4')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # Marcel Holtmann <marcel@holtmann.org>


prepare() {
  cd bluez-${pkgver}
  sed -i 's/ALSA/TINYALSA/g' \
    configure.ac Makefile.{am,plugins}
  sed -i 's/alsa/tinyalsa/g' \
    configure.ac Makefile.{am,plugins}
  sed -i 's/#include <alsa/#include <tinyalsa/' \
    profiles/midi/{libmidi.h,midi.c}
}

build() {
  cd bluez-${pkgver}
  autoreconf -ivf
  ./configure \
          --prefix=/usr \
          --mandir=/usr/share/man \
          --sysconfdir=/etc \
          --localstatedir=/var \
          --libexecdir=/usr/lib \
          --with-dbusconfdir=/usr/share \
          --enable-btpclient \
          --enable-midi \
          --enable-sixaxis \
          --enable-mesh \
          --enable-hid2hci \
          --enable-experimental \
          --enable-library # this is deprecated
  make
}

check() {
  cd bluez-$pkgver
  # tests segfault and hang
#  make check || /bin/true # https://bugzilla.kernel.org/show_bug.cgi?id=196621
}


package_bluez-tinyalsa() {
  pkgdesc="Daemons for the bluetooth protocol stack"
  depends=('libical' 'dbus' 'glib2' 'tinyalsa' 'json-c')
  backup=('etc/bluetooth/main.conf')
  conflicts=('obexd-client' 'obexd-server')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-pkglibexecPROGRAMS \
       install-dbussessionbusDATA \
       install-systemdsystemunitDATA \
       install-systemduserunitDATA \
       install-dbussystembusDATA \
       install-dbusDATA \
       install-man8

  # ship upstream main config file
  install -dm755 "${pkgdir}"/etc/bluetooth
  install -Dm644 "${srcdir}"/bluez-${pkgver}/src/main.conf "${pkgdir}"/etc/bluetooth/main.conf

  # add basic documention
  install -dm755 "${pkgdir}"/usr/share/doc/bluez/dbus-apis
  cp -a doc/*.txt "${pkgdir}"/usr/share/doc/bluez/dbus-apis/
  # fix module loading errors
  install -dm755 "${pkgdir}"/usr/lib/modprobe.d
  install -Dm644 "${srcdir}"/bluetooth.modprobe "${pkgdir}"/usr/lib/modprobe.d/bluetooth-usb.conf
  # load module at system start required by some functions
  # https://bugzilla.kernel.org/show_bug.cgi?id=196621
  install -dm755 "$pkgdir"/usr/lib/modules-load.d
  echo "crypto_user" > "$pkgdir"/usr/lib/modules-load.d/bluez.conf
  
  # fix obex file transfer - https://bugs.archlinux.org/task/45816
  ln -fs /usr/lib/systemd/user/obex.service "${pkgdir}"/usr/lib/systemd/user/dbus-org.bluez.obex.service
}

package_bluez-utils-tinyalsa() {
  pkgdesc="Development and debugging utilities for the bluetooth protocol stack"
  depends=('dbus' 'systemd' 'glib2')
  optdepends=('ell: for btpclient')
  conflicts=('bluez-hcidump')
  provides=('bluez-hcidump')
  replaces=('bluez-hcidump' 'bluez<=4.101')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  cd bluez-${pkgver}
  make DESTDIR="${pkgdir}" \
       install-binPROGRAMS \
       install-dist_zshcompletionDATA \
       install-man1

  # add missing tools FS#41132, FS#41687, FS#42716
  for files in `find tools/ -type f -perm -755`; do
    filename=$(basename $files)
    install -Dm755 "${srcdir}"/bluez-${pkgver}/tools/$filename "${pkgdir}"/usr/bin/$filename
  done
  
  # libbluetooth.so* are part of libLTLIBRARIES and binPROGRAMS targets
  #make DESTDIR=${pkgdir} uninstall-libLTLIBRARIES
  #rmdir ${pkgdir}/usr/lib
  rm -rf "${pkgdir}"/usr/lib
  
  # move the hid2hci man page out
  mv "${pkgdir}"/usr/share/man/man1/hid2hci.1 "${srcdir}"/
}

package_bluez-libs-tinyalsa() {
  pkgdesc="Deprecated libraries for the bluetooth protocol stack"
  depends=('glibc')
  provides=('libbluetooth.so')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  license=('LGPL2.1')

  cd bluez-${pkgver}
  make DESTDIR="${pkgdir}" \
       install-pkgincludeHEADERS \
       install-libLTLIBRARIES \
       install-pkgconfigDATA
}

package_bluez-cups-tinyalsa() {
  pkgdesc="CUPS printer backend for Bluetooth printers"
  depends=('cups')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  cd bluez-${pkgver}
  make DESTDIR="${pkgdir}" install-cupsPROGRAMS
}

package_bluez-hid2hci-tinyalsa() {
  pkgdesc="Put HID proxying bluetooth HCI's into HCI mode"
  depends=('systemd')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  cd bluez-${pkgver}
  make DESTDIR=${pkgdir} \
       install-udevPROGRAMS \
       install-rulesDATA
  
  install -dm755 "${pkgdir}"/usr/share/man/man1
  mv "${srcdir}"/hid2hci.1 "${pkgdir}"/usr/share/man/man1/hid2hci.1
}

package_bluez-plugins-tinyalsa() {
  pkgdesc="bluez plugins (PS3 Sixaxis controller)"
  depends=('systemd')
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  cd bluez-${pkgver}
  make DESTDIR="${pkgdir}" \
       install-pluginLTLIBRARIES
}
