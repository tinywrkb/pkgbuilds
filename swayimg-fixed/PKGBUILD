# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Artem Senichev <artemsen@gmail.com>

pkgname=swayimg-fixed
pkgver=1.3
pkgrel=1
pkgdesc='Image viewer for Sway/Wayland'
arch=('x86_64')
license=('MIT')
makedepends=('bash-completion' 'git' 'meson' 'wayland-protocols')
depends=('hicolor-icon-theme' 'json-c' 'librsvg' 'libwebp' 'libxkbcommon' 'wayland')
optdepends=('bash-completion: Bash completions')
url='https://github.com/artemsen/swayimg'
_commit=fddb164f328fc87eb40e4d00fea63704dc0898bd  # tags/1.3
source=("git+https://github.com/artemsen/swayimg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd swayimg
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
    local meson_options=(
      -D jpeg=enabled
      -D gif=enabled
      -D svg=enabled
      -D webp=enabled
      -D avif=enabled
      -D bash=enabled
      -D man=true
      -D desktop=true
    )
    arch-meson swayimg build "${meson_options[@]}"
    meson compile -C build
}

package(){
    meson install -C build --destdir "${pkgdir}"
    install -Dm644 swayimg/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
