# Maintainer: tinywrkb <tinywrkb@gmail.com>

pkgname=grub-editenv
pkgver=2.06
pkgrel=1
pkgdesc='edit grub env'
arch=('x86_64')
url='https://www.gnu.org/software/grub/'
license=('GPL3')
source=("https://ftp.gnu.org/gnu/grub/grub-${pkgver}.tar.gz")
sha256sums=('23b64b4c741569f9426ed2e3d0e6780796fca081bee4c99f62aa3f53ae803f5f')
depends=('glibc')
makedepends=('help2man' 'python' 'texinfo')
conflicts=('grub')

build() {
  cd grub-${pkgver}

  ./autogen.sh
  ./configure \
    --disable-werror \
    --with-platform=none

  make -C grub-core/lib/gnulib
  make \
    grub_script.tab.h \
    grub_script.yy.h \
    grub-editenv \
    grub-editenv.1
}

package() {
  cd grub-${pkgver}

  install -Dm755 grub-editenv -t ${pkgdir}/usr/bin/
  install -Dm644 grub-editenv.1 -t ${pkgdir}/usr/share/man/man1/
}
