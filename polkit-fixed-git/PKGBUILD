# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Aline Freitas <aline@alinefreitas.com.br>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=polkit-fixed-git
pkgver=0.120.r2.g2ae10cb
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges, git version"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd js78)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python-dbusmock)
backup=(etc/pam.d/polkit-1)
provides=(polkit)
conflicts=(polkit)
source=("polkit::git+https://gitlab.freedesktop.org/polkit/polkit.git")
sha256sums=('SKIP')

pkgver() {
  cd polkit
  git describe --tags --long | sed -r 's,([^-]*-g),r\1,;s,-,.,g'
}

prepare() {
  cd polkit
  # https://gitlab.freedesktop.org/polkit/polkit/-/merge_requests/99
  git remote add upstream https://gitlab.freedesktop.org/polkit/polkit.git
  git fetch upstream merge-requests/99/merge
  git merge FETCH_HEAD
}

build() {
  local meson_options=(
    --prefix=/usr
    -D session_tracking=libsystemd-login
    -D os_type=redhat
    -D examples=true
    -D tests=true
    -D gtk_doc=true
    -D man=true
  )
  arch-meson polkit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -d -o root -g 102 -m 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  mv "$pkgdir"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}

# vim: ts=2 sw=2 et:
