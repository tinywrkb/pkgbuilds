# Maintainer: tinywrkb <tinywrkb@gmail.com>

pkgname=uosys-mods
pkgver=2022.02.04.1
pkgrel=1
pkgdes='uosys mods'
arch=('any')
depends=('patch' 'rsync')
license=('GPL')
url=''
source=(os-mods{,-install} hook.in sync-pacman-db.hook uosys-pacman-db.service)
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  mkdir build

  # install.hook
  sed \
    -e 's/\$\$ALPM_OP\$\$/Install/' \
    -e 's/\$\$ALPM_DESC\$\$/OS mods in progress.../' \
    -e 's/\$\$ALPM_TRANS\$\$/PostTransaction/' \
    -e 's/\$\$MOD_TYPE\$\$/post/' \
    hook.in > build/install.hook

  # restore.hook
  sed \
    -e 's/\$\$ALPM_OP\$\$/Remove/' \
    -e 's/\$\$ALPM_DESC\$\$/Reverting OS mods.../' \
    -e 's/\$\$ALPM_TRANS\$\$/PreTransaction/' \
    -e 's/\$\$MOD_TYPE\$\$/pre/' \
    hook.in > build/restore.hook
}

package() {
  # os-mod-install lib
  install -Dm644 os-mods-install -t $pkgdir/usr/lib/os/

  # directory tree structure
  install -dm755 $pkgdir/usr/lib/os/{mods,patches,scripts}
  install -dm755 $pkgdir/usr/share/etc

  # os-mods alpm script
  install -Dm755 os-mods -t $pkgdir/usr/share/libalpm/scripts/

  # os-mods alpm hook
  install -Dm644 build/install.hook $pkgdir/usr/share/libalpm/hooks/00-${pkgname}-install.hook
  install -Dm644 build/restore.hook $pkgdir/usr/share/libalpm/hooks/00-${pkgname}-restore.hook

  # pacman db sync hook
  install -Dm644 sync-pacman-db.hook $pkgdir/usr/share/libalpm/hooks/zz-sync-pacman-db.hook

  # pacman db sync service
  install -Dm644 uosys-pacman-db.service -t $pkgdir/usr/lib/systemd/system
  mkdir -p  $pkgdir/usr/lib/systemd/system/multi-user.target.wants/
  ln -s ../uosys-pacman-db.service \
    $pkgdir/usr/lib/systemd/system/multi-user.target.wants/uosys-pacman-db.service
}
