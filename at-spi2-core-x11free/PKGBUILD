# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgname=at-spi2-core
pkgname=${_pkgname}-x11free
pkgver=2.40.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(dbus glib2 libsystemd)
makedepends=(gobject-introspection git meson dbus-broker systemd)
optdepends=('dbus-broker: Alternative bus implementation')
conflicts=($_pkgname)
provides=($_pkgname)
_commit=baf8608964e84185f61d9424af3979a9a6e15a7c  # tags/AT_SPI2_CORE_2_40_1^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $_pkgname
}

build() {
  arch-meson $_pkgname build \
    -D default_bus=dbus-broker \
    -D x11=no \
    -D docs=false
  meson compile -C build
}

check() {
  # memory test fails without desktop
  # broker fails to launch without journald
  dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
