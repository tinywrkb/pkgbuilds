# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

_pkgname=xdg-desktop-portal-gtk
pkgname=$_pkgname-gnomefree
pkgver=1.8.0
pkgrel=1
pkgdesc="A GTK+ backend for xdg-desktop-portal"
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
arch=(x86_64)
license=(LGPL)
# TODO: gnome-keyring dependency is wrong, need to be removed
#       https://bugs.archlinux.org/task/66561?project=1&string=xdg-desktop-portal-gtk
depends=(gtk3-nonsensefree gnome-keyring)
makedepends=(xdg-desktop-portal python git)
optdepends=("evince-previewer: Print preview")
conflicts=($_pkgname)
provides=($_pkgname xdg-desktop-portal-impl)
_commit=5e45e780eecacfe199ee9582cce4c54a27ce186f  # tags/1.8.0^0
source=("git+https://github.com/flatpak/xdg-desktop-portal-gtk#commit=$_commit"
        '0001-gnome-free-portals.patch')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname

  # https://github.com/flatpak/xdg-desktop-portal-gtk/pull/345
  patch -Np1 -i ../0001-gnome-free-portals.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  # TODO: confirm that appchooser and settings are relevant for gnomeless or should be disabled
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-background \
    --disable-lockdown \
    --disable-screencast \
    --disable-screenshot \
    --disable-wallpaper \
    --enable-appchooser \
    --enable-settings \

  make
}

check() {
  cd $_pkgname
  make check
}

package() {
  cd $_pkgname
  DESTDIR="$pkgdir" make install
}
