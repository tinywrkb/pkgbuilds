# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

_pkgname=xdg-desktop-portal-gtk
pkgname=$_pkgname-gnomefree
pkgver=1.10.0
pkgrel=1
pkgdesc="A GTK+ backend for xdg-desktop-portal"
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
arch=(x86_64)
license=(LGPL)
# TODO: gnome-keyring dependency is wrong, need to be removed
#       https://bugs.archlinux.org/task/66561?project=1&string=xdg-desktop-portal-gtk
depends=(gtk3-nonsensefree gnome-keyring)
makedepends=(xdg-desktop-portal python git)
optdepends=("evince-previewer: Print preview")
conflicts=($_pkgname)
provides=($_pkgname xdg-desktop-portal-impl)
_commit=3a9ac80eb720d1ba8a2aecc2108f66b2f737f185  # tags/1.10.0^0
source=("git+https://github.com/flatpak/xdg-desktop-portal-gtk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  git revert -n 7882b586448dd81af7029981eee93a9871240a95
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  local config_options=(
    --prefix=/usr
    --libexecdir=/usr/lib

    --disable-background
    --disable-lockdown
    --disable-screencast
    --disable-screenshot
    --disable-wallpaper
    --enable-appchooser
    --enable-settings
  )
  ./configure "${config_options[@]}"
  make
}

check() {
  cd $_pkgname
  make check
}

package() {
  cd $_pkgname
  DESTDIR="$pkgdir" make install
}
