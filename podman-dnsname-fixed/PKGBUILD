# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Morten Linderud <foxboron@archlinux.org>

# man:containers.conf(5)
#  cni_plugin_dirs=[]
#   List of paths to directories where CNI plugin binaries are located.
#   The default list is:
#     cni_plugin_dirs = [
#       ...
#       "/usr/lib/cni",
#       ...
#     ]

pkgname=podman-dnsname-fixed
pkgver=1.3.1
pkgrel=2
pkgdesc="name resolution for containers"
arch=(x86_64)
url="https://github.com/containers/dnsname"
license=(APACHE)
makedepends=(git go)
conflicts=(podman-dnsname)
provides=(podman-dnsname)
options=(!lto)
_commit=18822f9a4fb35d1349eb256f4cd2bfd372474d84  # tags/v1.3.1
source=("podman-dnsname::git+https://github.com/containers/dnsname.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
    cd podman-dnsname
    git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"

  cd podman-dnsname
  make
}

package() {
  depends+=(dnsmasq)
  cd podman-dnsname
  make PREFIX="/usr" LIBEXECDIR="/usr/lib/cni" DESTDIR="${pkgdir}" install
}
