# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-nonsensefree
pkgver=3.24.36
pkgrel=1
epoch=1
pkgdesc="GTK3 without Tracker and Cloudproviders"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(
  adwaita-icon-theme
  atk
  cairo
  cantarell-fonts
  dconf
  desktop-file-utils
  fontconfig
  fribidi
  gdk-pixbuf2
  glib2
  harfbuzz
  iso-codes
  libcolord
  libcups
  libegl
  libepoxy
  libgl
  librsvg
  libxcomposite
  libx11
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxinerama
  libxkbcommon
  libxrandr
  libxrender
  pango
  shared-mime-info
  wayland
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  sassc
  wayland-protocols
)
provides=(gtk3)
conflicts=(gtk3)
license=(LGPL)
install=gtk3.install
_commit=35516a5e866cd77390eb71d72d5b67f45dc4302c  # tags/3.24.36^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
  gtk-query-immodules-3.0.hook
)
sha256sums=('SKIP'
            'a0319b6795410f06d38de1e8695a9bf9636ff2169f40701671580e60a108e229')

pkgver() {
  cd gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}


prepare() {
  cd gtk
}

build() {
  local meson_options=(
    -D x11_backend=true
    -D wayland_backend=true
    -D broadway_backend=false
    -D xinerama=yes
    -D cloudproviders=false
    -D tracker3=false
    -D print_backends=auto
    -D colord=yes
    -D gtk_doc=false
    -D man=true
    -D introspection=true
    -D demos=false
    -D examples=false
    -D tests=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(gtk-update-icon-cache)
  optdepends=('evince-previewer: Default print preview command')
  provides+=(
    gtk3-print-backends
    libgailutil-3.so
    libgdk-3.so
    libgtk-3.so
  )
  conflicts+=(gtk3-print-backends)
  replaces+=("gtk3-print-backends<=3.22.26-1")

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  cd "$pkgdir"

  # Built by GTK 4, shared with GTK 3
  rm -f usr/bin/gtk-update-icon-cache
  rm usr/share/man/man1/gtk-update-icon-cache.1
}

# vim:set ts=2 sw=2 et:
