# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-nonsensefree
pkgver=3.24.35
pkgrel=2
epoch=1
pkgdesc="GTK3 without Tracker and Cloudproviders"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(
  adwaita-icon-theme
  at-spi2-atk
  atk
  cairo
  cantarell-fonts
  dconf
  desktop-file-utils
  fribidi
  gdk-pixbuf2
  gtk-update-icon-cache
  iso-codes
  libcolord
  libcups
  libepoxy
  librsvg
  libxcomposite
  libxcursor
  libxdamage
  libxi
  libxinerama
  libxkbcommon
  libxrandr
  mesa
  pango
  shared-mime-info
  wayland
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  sassc
  wayland-protocols
)
provides=(gtk3)
conflicts=(gtk3)
license=(LGPL)
install=gtk3.install
_commit=14cf55f98ddd71ad3f91487eda1c7f14d67de119  # tags/3.24.35^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
  gtk-query-immodules-3.0.hook
)
sha256sums=('SKIP'
            'a0319b6795410f06d38de1e8695a9bf9636ff2169f40701671580e60a108e229')

pkgver() {
  cd gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}


prepare() {
  cd gtk

  # Crash in gnome-screenshot
  # https://gitlab.gnome.org/GNOME/gtk/-/issues/4456
  git cherry-pick -n e413f5c43259a22269aa1e75767dd545e10119b5

  # https://bugs.archlinux.org/task/76651
  # https://gitlab.gnome.org/GNOME/gtk/-/issues/5365
  git cherry-pick -n 3f1536632f682c355d0c3abe1afc80cf975c2cce
}

build() {
  local meson_options=(
    -D x11_backend=true
    -D wayland_backend=true
    -D broadway_backend=false
    -D xinerama=yes
    -D cloudproviders=false
    -D tracker3=false
    -D print_backends=auto
    -D colord=yes
    -D gtk_doc=false
    -D man=true
    -D introspection=true
    -D demos=false
    -D examples=false
    -D tests=false
  )

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  optdepends=('evince-previewer: Default print preview command')
  provides+=(gtk3-print-backends libgtk-3.so libgdk-3.so libgailutil-3.so)
  conflicts+=(gtk3-print-backends)
  replaces+=("gtk3-print-backends<=3.22.26-1")

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  cd "$pkgdir"

  # TODO: remove this?
  rm -f usr/bin/gtk-update-icon-cache
  rm usr/share/man/man1/gtk-update-icon-cache.1
}

# vim:set ts=2 sw=2 et:
