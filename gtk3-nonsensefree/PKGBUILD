# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-nonsensefree
pkgver=3.24.28
pkgrel=3
epoch=1
pkgdesc="GTK3 without X11, Tracker, and Cloudproviders"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(atk cairo libepoxy gdk-pixbuf2
         dconf pango shared-mime-info at-spi2-atk-x11free
         wayland libxkbcommon adwaita-icon-theme json-glib librsvg
         wayland-protocols desktop-file-utils mesa cantarell-fonts colord rest
         libcups libcanberra fribidi iso-codes gtk-update-icon-cache)
makedepends=(gobject-introspection docbook-xsl git sassc meson)
provides=(gtk3)
conflicts=(gtk3)
license=(LGPL)
install=gtk3.install
_commit=23db3508899f8304482b68e04a181bb081549081  # tags/3.24.28^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
        gtk-query-immodules-3.0.hook)
sha256sums=('SKIP'
            'a0319b6795410f06d38de1e8695a9bf9636ff2169f40701671580e60a108e229')

pkgver() {
  cd gtk
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  # https://gitlab.gnome.org/GNOME/gtk/-/commit/df4b564d69cc7d2e751537eff61259b36f37e9e5
  CFLAGS+=" -DG_ENABLE_DEBUG -DG_DISABLE_CAST_CHECKS -DG_DISABLE_ASSERT"

  arch-meson gtk build \
    -D x11_backend=false \
    -D wayland_backend=true \
    -D broadway_backend=false \
    -D xinerama=no \
    -D cloudproviders=false \
    -D tracker3=false \
    -D print_backends=auto \
    -D colord=yes \
    -D gtk_doc=false \
    -D man=true \
    -D introspection=true \
    -D demos=false \
    -D examples=false \
    -D tests=false \

  meson compile -C build
}

package() {
  provides+=(gtk3-print-backends libgtk-3.so libgdk-3.so libgailutil-3.so)
  conflicts+=(gtk3-print-backends)
  replaces+=("gtk3-print-backends<=3.22.26-1")

  DESTDIR="$pkgdir" meson install -C build

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  cd "$pkgdir"

  rm usr/bin/gtk-update-icon-cache
  rm usr/share/man/man1/gtk-update-icon-cache.1
  rm usr/share/man/man1/gtk3-{demo,demo-application,icon-browser,widget-factory}.1
}

# vim:set ts=2 sw=2 et:
