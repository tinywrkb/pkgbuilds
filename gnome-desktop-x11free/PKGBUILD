# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jan@archlinux.org>

_pkgname=gnome-desktop
pkgname=$_pkgname-x11free
pkgver=3.40.0
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
url="https://gitlab.gnome.org/GNOME/gnome-desktop"
arch=(x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3-nonsensefree xkeyboard-config iso-codes libseccomp
         bubblewrap systemd-libs libxkbcommon)
makedepends=(gobject-introspection git meson gtk-doc yelp-tools)
checkdepends=(xorg-server-xvfb)
conflicts=($_pkgname)
provides=($_pkgname libgnome-desktop-3.so)
_commit=7e5a95616ebce37999f37bbf3460cd8b674ae62f  # tags/3.40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-desktop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname
}

build() {
  arch-meson $_pkgname build \
    -D gnome_distributor="Arch Linux" \
    -D gtk_doc=false \
    -D debug_tools=false \
    -D date_in_gnome_version=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
