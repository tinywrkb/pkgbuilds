# Maintainer: tinywrkb <tinywrkb@gmail.com>

_pkgname=sway
pkgname=uosys-mods-${_pkgname}
pkgver=2022.02.05.1
pkgrel=1
pkgdesc="$_pkgname uosys mods"
arch=('any')
license=('GPL')
url=''
depends=('uosys-mods')
source=(conf-{common,shell,systemd}.conf
        conf.d-0{0-init-update-environment,1-init-set-variables}.conf
        conf.d-1{0-session-output,1-session-input,2-session-seat}.conf
        conf.d-2{0-appearance,1-statusbar,2-launcher}.conf
        conf.d-2{3-containers,4-workspaces,5-modes,6-scratchpads,7-windows}.conf
        conf.d-4{0-keybindings-session,1-keybindings-media,2-keybindings-extra}.conf
        conf.d-6{0-startup-applications,1-startup-services,2-startup-scratchpads}.conf
        start-sway{,-session}
        user-tmpfiles
        wm{bar{,-status,-toggle-hide},flash,kbd,snap})
sha256sums=('SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP'
            'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

package() {
  depends+=($_pkgname)
  install -Dm644 user-tmpfiles ${pkgdir}/usr/share/user-tmpfiles.d/sway.conf

  for f in conf-*.conf; do
    install -Dm644 $f ${pkgdir}/etc/sway/${f#conf-}
  done

  for f in conf.d-*.conf; do
    install -Dm644 $f ${pkgdir}/etc/sway/conf.d/${f#conf.d-}
  done

  for f in start-sway{,-session} wm{bar{,-status,-toggle-hide},flash,kbd,snap}; do
    install -Dm755 $f -t ${pkgdir}/usr/bin/
  done
}
