# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_pkgname=libpulse
pkgname=${_pkgname}-x11free
pkgdesc="PulseAudio client library"
pkgver=14.99.2
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(libasyncns libsndfile systemd)
optdepends=('glib2: mainloop integration')
makedepends=(git meson xmltoman)
conflicts=($_pkgname)
provides=(libpulse{,-simple,-mainloop-glib}.so)
provides+=($_pkgname)
backup=(etc/pulse/client.conf)
_commit=ce962563eba7ee18ad4641f700d097adbea6b6d7  # tags/v14.99.2
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
  #git describe --tags | sed 's/^v//;s/-/+/g' | sed 's/^14\..+/14.99+/'
}

build() {
  local meson_options=(
    -D daemon=false
    -D doxygen=false
    -D tests=false

    -D database=simple
    -D stream-restore-clear-old-devices=true

    # paths
    -D pulsedsp-location='/usr/\$LIB/pulseaudio'
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D zshcompletiondir=no

    # optional features
    -D alsa=disabled
    -D asyncns=disabled
    -D avahi=disabled
    -D bluez5=false
    -D bluez5-gstreamer=disabled
    -D bluez5-native-headset=false
    -D bluez5-ofono-headset=false
    -D dbus=enabled
    -D elogind=disabled
    -D fftw=disabled
    -D glib=enabled
    -D gsettings=disabled
    -D gstreamer=disabled
    -D gtk=disabled
    -D ipv6=false
    -D jack=disabled
    -D lirc=disabled
    -D openssl=disabled
    -D orc=disabled
    -D oss-output=disabled
    -D samplerate=disabled
    -D soxr=disabled
    # speex is enabled as at least one echo impl is needed, that's unless meson.build is patched
    # doesn't change the depends list as speex is a dependency of libsndfile
    -D speex=enabled
    -D tcpwrap=disabled
    -D valgrind=disabled
    -D x11=disabled

    # echo cancellation
    -D adrian-aec=false
    -D webrtc-aec=disabled
  )

  arch-meson pulseaudio build  "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
