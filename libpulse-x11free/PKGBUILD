# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_pkgname=libpulse
pkgname=${_pkgname}-x11free
pkgdesc="PulseAudio client library"
pkgver=14.0+274+gfec9eb178
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(dbus libasyncns libcap libsndfile systemd)
optdepends=('glib2: mainloop integration')
makedepends=(git meson xmltoman)
conflicts=($_pkgname)
provides=(libpulse{,-simple,-mainloop-glib}.so)
provides+=($_pkgname)
backup=(etc/pulse/client.conf)
_commit=fec9eb178d5fee5b39cd49d5d55a7ac202bb9894  # 14.0+274+gfec9eb178
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit" build-sys-meson-allow-building-client-libraries-only.patch)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pulseaudio

  patch -Np1 -i ../build-sys-meson-allow-building-client-libraries-only.patch
  sed -i "/subdir('doxygen')/d" meson.build
}

build() {
  arch-meson pulseaudio build \
    -D pulsedsp-location='/usr/\$LIB/pulseaudio' \
    -D stream-restore-clear-old-devices=true \
    -D udevrulesdir=/usr/lib/udev/rules.d \
    -D daemon=false \
    -D tests=false \
    -D database=simple \
    -D zshcompletiondir=no \
    -D alsa=disabled \
    -D asyncns=disabled \
    -D avahi=disabled \
    -D bluez5=false \
    -D bluez5-gstreamer=disabled \
    -D bluez5-native-headset=false \
    -D bluez5-ofono-headset=false \
    -D fftw=disabled \
    -D elogind=disabled \
    -D glib=enabled \
    -D gsettings=disabled \
    -D gstreamer=disabled \
    -D gtk=disabled \
    -D ipv6=false \
    -D jack=disabled \
    -D lirc=disabled \
    -D openssl=disabled \
    -D orc=disabled \
    -D oss-output=auto \
    -D samplerate=disabled \
    -D soxr=disabled \
    -D speex=enabled \
    -D tcpwrap=disabled \
    -D valgrind=disabled \
    -D x11=disabled \
    -D adrian-aec=false \
    -D webrtc-aec=disabled \

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  cd "$pkgdir"/usr
  rm bin/pa-info

  # not sure this is working, so disable for now
  # keeping  the default means conflicting with pulseaudio but it's not declared
  #  to avoid unneeded conflict with pipewire-pulse

  #mv share/bash-completion/completions/{pulseaudio,libpulse}
  #for f in pacat pactl padsp paplay parec parecord; do
  #  rm share/bash-completion/completions/${f}
  #  ln -s libpulse share/bash-completion/completions/${f}
  #done

  rm share/locale/*/LC_MESSAGES/pulseaudio.mo
  rmdir share/locale/*/LC_MESSAGES
  rmdir share/locale/*
  rmdir share/locale
  rm share/man/man1/pax11publish.1
}

# vim:set sw=2 et:
