# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_pkgname=libpulse
pkgname=${_pkgname}-x11free
pkgdesc="PulseAudio client library"
pkgver=14.99+280+gf760143c2
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(dbus libasyncns libcap libsndfile systemd)
optdepends=('glib2: mainloop integration')
makedepends=(git meson xmltoman)
conflicts=($_pkgname)
provides=(libpulse{,-simple,-mainloop-glib}.so)
provides+=($_pkgname)
backup=(etc/pulse/client.conf)
_commit=f760143c2238cc6d0efbc76ab64786d7bf481645  # 14.99+280+gf760143c2
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g' | sed 's/^14\..+/14.99+/'
}

prepare() {
  cd pulseaudio

  sed -i "/subdir('doxygen')/d" meson.build
}

build() {
  arch-meson pulseaudio build \
    -D pulsedsp-location='/usr/\$LIB/pulseaudio' \
    -D stream-restore-clear-old-devices=true \
    -D udevrulesdir=/usr/lib/udev/rules.d \
    -D daemon=false \
    -D tests=false \
    -D database=simple \
    -D zshcompletiondir=no \
    -D alsa=disabled \
    -D asyncns=disabled \
    -D avahi=disabled \
    -D bluez5=false \
    -D bluez5-gstreamer=disabled \
    -D bluez5-native-headset=false \
    -D bluez5-ofono-headset=false \
    -D fftw=disabled \
    -D elogind=disabled \
    -D glib=enabled \
    -D gsettings=disabled \
    -D gstreamer=disabled \
    -D gtk=disabled \
    -D ipv6=false \
    -D jack=disabled \
    -D lirc=disabled \
    -D openssl=disabled \
    -D orc=disabled \
    -D oss-output=auto \
    -D samplerate=disabled \
    -D soxr=disabled \
    -D speex=enabled \
    -D tcpwrap=disabled \
    -D valgrind=disabled \
    -D x11=disabled \
    -D adrian-aec=false \
    -D webrtc-aec=disabled \

  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
