# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# removed depends: libspectre gsfonts poppler-glib
_pkgname=evince
pkgname=$_pkgname-previewer
pkgver=40.2
pkgrel=1
pkgdesc="Document viewer (PDF, PostScript, XPS, djvu, dvi, tiff, cbr, cbz, cb7, cbt)"
url="https://wiki.gnome.org/Apps/Evince"
arch=(x86_64)
license=(GPL)
# TODO: might need libhandy linked against gtk3-nonsensefree
depends=(gtk3-nonsensefree libhandy poppler-glib)
makedepends=(python git meson appstream-glib)
conflicts=($_pkgname)
provides=(libev{document,view}3.so)
groups=(gnome)
_commit=864fd5d55be2fa96e2afce507b454ce018cdeaf8  # tags/40.2^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname
}
  
build() {
  arch-meson $_pkgname build \
    -D viewer=false \
    -D previewer=true \
    -D thumbnailer=false \
    -D browser_plugin=false \
    -D nautilus=false \
    -D comics=disabled \
    -D djvu=disabled \
    -D dvi=disabled \
    -D pdf=enabled \
    -D ps=enabled \
    -D tiff=disabled \
    -D xps=disabled \
    -D gtk_doc=false \
    -D user_doc=false \
    -D introspection=false \
    -D dbus=true \
    -D keyring=disabled \
    -D gtk_unix_print=enabled \
    -D thumbnail_cache=disabled \
    -D multimedia=disabled \
    -D gspell=disabled \
    -D t1lib=disabled \

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  # cleanup
  cd "$pkgdir"/usr
  rm -r \
    include \
    lib/{pkgconfig,systemd} \
    share/{dbus-1,metainfo}
  rm \
    share/applications/org.gnome.Evince.desktop \
    share/man/man1/evince{,-thumbnailer}.1
}
