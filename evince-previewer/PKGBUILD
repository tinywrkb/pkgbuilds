# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# removed depends: libspectre gsfonts poppler-glib
pkgname=evince-previewer
pkgver=41.4
pkgrel=1
pkgdesc="Print preview with the GTK Print portal implementation"
url="https://wiki.gnome.org/Apps/Evince"
arch=(x86_64)
license=(GPL)
# TODO: might need libhandy linked against gtk3-nonsensefree
depends=(gtk3-nonsensefree libhandy poppler-glib)
makedepends=(python git meson appstream-glib)
conflicts=(evince)
provides=(libev{document,view}3.so)
groups=(gnome)
_commit=27a1be1b599ac8e6021622739f79a25f24dfe635  # tags/41.4^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd evince
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd evince
  # Fix build with Meson 0.61
  git cherry-pick -n 1060b24d051607f14220f148d2f7723b29897a54
}
  
build() {
  local meson_options=(
    -D viewer=false
    -D previewer=true
    -D thumbnailer=false
    -D nautilus=false
    -D comics=disabled
    -D djvu=disabled
    -D dvi=disabled
    -D pdf=enabled
    -D ps=enabled
    -D tiff=disabled
    -D xps=disabled
    -D gtk_doc=false
    -D user_doc=false
    -D introspection=false
    -D dbus=true
    -D keyring=disabled
    -D gtk_unix_print=enabled
    -D thumbnail_cache=disabled
    -D multimedia=disabled
    -D gspell=disabled
    -D t1lib=disabled
  )
  arch-meson evince build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  # cleanup
  cd "$pkgdir"/usr
  rm -r \
    include \
    lib/{pkgconfig,systemd} \
    share/{dbus-1,metainfo}
  rm \
    share/applications/org.gnome.Evince.desktop \
    share/man/man1/evince{,-thumbnailer}.1
}
