# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: sl1pkn07 <sl1pkn07@gmail.com>

pkgbase=pipewire-fixed-git
pkgname=('pipewire-fixed-git'
         'alsa-card-profiles-fixed-git'
         'pipewire-docs-fixed-git'
         'pipewire-media-session-fixed-git'
         'pipewire-alsa-fixed-git'
         'pipewire-jack-fixed-git'
         'pipewire-pulse-fixed-git'
         )
pkgver=0.3.23.5.ge89e87ba
pkgrel=1
pkgdesc='Low-latency audio/video router and processor (GIT version)'
arch=('x86_64')
url='https://pipewire.org'
license=('LGPL')
makedepends=('git'
             'meson'
             'doxygen'
             'graphviz'
             'xmltoman'
             'valgrind'
             'libpulse'
             'alsa-lib'
             'sbc'
             'rtkit'
             'dbus'
             'sdl2'
             'ncurses'
             'libsndfile'
             'bluez-libs'
             'libldac'
             'libopenaptx'
             'libfdk-aac'
             )
checkdepends=('desktop-file-utils')
source=('git+https://gitlab.freedesktop.org/pipewire/pipewire.git')
sha256sums=('SKIP')

pkgver() {
  cd pipewire
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build

  cd pipewire

  # Reduce docs size
  printf '%s\n' >> doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  cd "${srcdir}/build"

  CFLAGS+=" -Wformat"

  arch-meson ../pipewire \
    -D udevrulesdir=/usr/lib/udev/rules.d \
    -D docs=true \
    -D jack=false \
    -D gstreamer=false \

  ninja
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="${1}" f d; shift
  for f; do
    d="${srcdir}/${p}/${f#$pkgdir/}"
    mkdir -p "$(dirname "${d}")"
    mv "${f}" "${d}"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "${f}")"
  done
}

package_pipewire-fixed-git() {
  depends=('alsa-card-profiles-fixed-git'
           'rtkit'
           'libasound.so'
           'libbluetooth.so'
           'libdbus-1.so'
           'libfdk-aac.so'
           'libldacBT_enc.so'
           'libncursesw.so'
           'libopenaptx.so'
           'libsbc.so'
           'libsndfile.so'
           'libsystemd.so'
           'libudev.so'
           )
  optdepends=('pipewire-docs-fixed-git: Documentation'
              'pipewire-media-session-fixed-git: Default session manager'
              'pipewire-alsa-fixed-git: ALSA configuration'
              'pipewire-jack-fixed-git: JACK support'
              'pipewire-pulse-fixed-git: PulseAudio replacement'
              'gst-plugin-pipewire-fixed-git: gstreamer support'
              )
  provides=('pipewire'
            "libpipewire-${pkgver:0:3}.so"
            )
  conflicts=('pipewire')
  backup=(etc/pipewire/{pipewire{,-pulse},client{,-rt}}.conf)
  install=pipewire.install

  DESTDIR="${pkgdir}" meson install -C build

  cd "${pkgdir}"

  _pick acp usr/lib/udev
  _pick acp usr/share/alsa-card-profile

  _pick docs usr/share/doc

  _pick pms etc/pipewire/media-session.d/*.conf
  _pick pms usr/bin/pipewire-media-session
  _pick pms usr/lib/systemd/user/pipewire-media-session.service

  _pick jack etc/pipewire/{jack.conf,media-session.d/with-jack}
  _pick jack usr/bin/pw-jack usr/lib/pipewire-${pkgver:0:3}/jack
  _pick jack usr/share/man/man1/pw-jack.1

  _pick pulse etc/pipewire/media-session.d/with-pulseaudio
}

package_alsa-card-profiles-fixed-git() {
  pkgdesc+=" - ALSA card profiles (GIT version)"
  provides=('alsa-card-profiles')
  conflicts=('alsa-card-profiles')
  arch=('any')

  mv acp/* "${pkgdir}"
}

package_pipewire-docs-fixed-git() {
  pkgdesc+=" - documentation (GIT Version)"
  provides=('pipewire-docs')
  conflicts=('pipewire-docs')
  arch=('any')

  mv docs/* "${pkgdir}"
}

package_pipewire-media-session-fixed-git() {
  pkgdesc+=" - Session manager"
  depends=(pipewire libpipewire-$_ver.so)
  provides=('pipewire-media-session')
  conflicts=('pipewire-media-session')
  backup=(etc/pipewire/media-session.d/media-session.conf
          etc/pipewire/media-session.d/{alsa,bluez,v4l2}-monitor.conf)
  install=pipewire-media-session.install
  mv pms/* "$pkgdir"
}

package_pipewire-alsa-fixed-git() {
  pkgdesc+=" - ALSA configuration (GIT version)"
  depends=('pipewire-media-session')
  provides=('pipewire-alsa' 'pulseaudio-alsa')
  conflicts=('pipewire-alsa')
  arch=('any')

  mkdir -p "${pkgdir}/etc/alsa/conf.d"
  ln -st "${pkgdir}/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/{50-pipewire,99-pipewire-default}.conf

  install -Dm644 /dev/null "${pkgdir}/etc/pipewire/media-session.d/with-alsa"
}

package_pipewire-jack-fixed-git() {
  pkgdesc+=" - JACK support (GIT version)"
  depends=('pipewire-media-session'
           "libpipewire-${pkgver:0:3}.so"
           )
  backup=(etc/pipewire/jack.conf)
  provides=('pipewire-jack')
  conflicts=('pipewire-jack')

  mv jack/* "${pkgdir}"
}

package_pipewire-pulse-fixed-git() {
  pkgdesc+=" - PulseAudio replacement (GIT version)"
  depends=('pipewire-media-session'
           'libpulse'
           )
  provides=('pipewire-pulse'
            'pulseaudio'
            'pulseaudio-bluetooth'
            )
  conflicts=('pipewire-pulse'
             'pulseaudio'
             'pulseaudio-bluetooth'
             )
  arch=('any')
  install=pipewire-pulse.install

  mv pulse/* "${pkgdir}"
}
