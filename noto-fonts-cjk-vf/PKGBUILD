# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgbase=noto-fonts-cjk-vf
pkgname=(noto-fonts-{cjk,{cjk-,}{hk,jp,kr,sc,tc}}-vf)
pkgver=20220126
pkgrel=1
pkgdesc='Google Noto CJK variable fonts'
url='https://www.google.com/get/noto/'
license=(custom:SIL)
arch=(any)
_commit=39abe091d7332e5a0c186b51209cd20df0733e0d
#source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/googlefonts/noto-cjk/archive/${_commit}.tar.gz"
source=("noto-cjk::git+https://github.com/googlefonts/noto-cjk.git#commit=${_commit}"
        70-noto-{cjk,hk,jp,kr,sc,tc}.conf)
sha256sums=('SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
makedepends=(git)
conflicts=(noto-fonts-cjk)
provides=(noto-fonts-cjk)

_langs=(hk jp kr sc tc)

package_noto-fonts-cjk-vf(){
  cd noto-cjk

  for _font in Sans Serif; do
    install -Dm644 \
      ${_font}/Variable/OTC/Noto${_font}CJK-VF.otf.ttc \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto${_font}CJK.ttc

    # mono is available only for sans
    [ "${_font}" = "Sans" ] || continue
    install -Dm644 \
      ${_font}/Variable/OTC/Noto${_font}MonoCJK-VF.otf.ttc \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto${_font}MonoCJKCJK.ttc
  done

  install -Dm644 -t "$pkgdir"/usr/share/fontconfig/conf.avail/ \
    ../70-noto-cjk.conf 
  install -dm755 "$pkgdir"/usr/share/fontconfig/conf.default
  ln -s  -t "$pkgdir"/usr/share/fontconfig/conf.default/ \
    ../conf.avail/70-noto-cjk.conf 

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}/
}

_package_locl(){
  conflicts+=(noto-fonts-cjk-vf)
  provides+=(noto-fonts-cjk-vf)

  cd noto-cjk

  for _font in Sans Serif; do
    install -Dm644 \
      ${_font}/Variable/OTF/Noto${_font}CJK${_lang}-VF.otf \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto${_font}CJK.otf

    # mono is available only for sans
    [ "${_font}" = "Sans" ] || continue
    install -Dm644 \
      ${_font}/Variable/OTF/Mono/Noto${_font}MonoCJK${_lang}-VF.otf \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto${_font}MonoCJK.otf
  done

  install -Dm644 -t "$pkgdir"/usr/share/fontconfig/conf.avail/ \
    ../70-noto-cjk.conf 
  install -dm755 "$pkgdir"/usr/share/fontconfig/conf.default
  ln -s  -t "$pkgdir"/usr/share/fontconfig/conf.default/ \
    ../conf.avail/70-noto-cjk.conf 

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}/
}

_package_subset(){
  # the conflict is due to the multi-lingual mono font
  conflicts+=(noto-fonts-cjk-vf)
  provides+=(noto-fonts-cjk-vf)

  cd noto-cjk

  for _font in Sans Serif; do
    install -Dm644 \
      ${_font}/Variable/OTF/Subset/Noto${_font}${_lang^^}-VF.otf \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto{_font}${_lang^^}.otf

    # mono is available only for sans
    [ "${_font}" = "Sans" ] || continue
    # there are no subset mono fonts, so install the multi-lingual with default locl
    install -Dm644 \
      ${_font}/Variable/OTF/Mono/Noto${_font}MonoCJK${_lang}-VF.otf \
      "$pkgdir"/usr/share/fonts/noto-cjk/Noto${_font}MonoCJK.otf
  done

  install -Dm644 -t "$pkgdir"/usr/share/fontconfig/conf.avail/ \
    ../70-noto-${_lang}.conf 
  install -dm755 "$pkgdir"/usr/share/fontconfig/conf.default
  ln -s  -t "$pkgdir"/usr/share/fontconfig/conf.default/ \
    ../conf.avail/70-noto-${_lang}.conf 

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}/
}

package_noto-fonts-cjk-hk-vf(){
  pkgdesc+=' - Traditional Chinese HK as default language (OpenType locl)'

  _lang=hk
  _package_locl
}

package_noto-fonts-cjk-jp-vf(){
  pkgdesc+=' - Japanese as default language (OpenType locl)'

  _lang=jp
  _package_locl
}

package_noto-fonts-cjk-kr-vf(){
  pkgdesc+=' - Korean as default language (OpenType locl)'

  _lang=kr
  _package_locl
}

package_noto-fonts-cjk-sc-vf(){
  pkgdesc+=' - Simplified Chinese as default language (OpenType locl)'

  _lang=sc
  _package_locl
}

package_noto-fonts-cjk-tc-vf(){
  pkgdesc+=' - Traditional Chinese as default language (OpenType locl)'

  _lang=tc
  _package_locl
}

package_noto-fonts-hk-vf(){
  pkgdesc='Google Noto Traditional Chinese HK variable fonts'

  _lang=hk
  _package_subset
}

package_noto-fonts-jp-vf(){
  pkgdesc='Google Noto Japanese variable fonts'

  _lang=jp
  _package_subset
}

package_noto-fonts-kr-vf(){
  pkgdesc='Google Noto Korean variable fonts'

  _lang=kr
  _package_subset
}

package_noto-fonts-sc-vf(){
  pkgdesc='Google Noto Simplified Chinese variable fonts'

  _lang=sc
  _package_subset
}

package_noto-fonts-tc-vf(){
  pkgdesc='Google Noto Traditional Chinese variable fonts'

  _lang=tc
  _package_subset
}
