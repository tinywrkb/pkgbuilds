# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-duktape
pkgver=0.120.r6.gb10a1bd
pkgrel=1
pkgdesc="polkit with duktape as the javascript engine"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd duktape-fixed)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python-dbusmock)
provides=(libpolkit-{agent,gobject}-1.so)
conflicts=(polkit)
provides=(polkit)
backup=(etc/pam.d/polkit-1)
_commit=b10a1bdb697045db40774f2a9a8c58ae5c7189c3  # 0.120.r6.gb10a1bd
source=("git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=$_commit"
        '0001-meson-0.61.patch')
sha256sums=('SKIP'
            '9a84b59d38b3f86c70c5fc3a28cbfe8d7a22cf190eb20ff433f7f53f5ff73a9a')

pkgver() {
  cd polkit
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd polkit

  # https://gitlab.freedesktop.org/polkit/polkit/-/merge_requests/99
  patch -Np1 -i ../0001-meson-0.61.patch
}

build() {
  local meson_options=(
    --prefix=/usr
    -D session_tracking=libsystemd-login
    -D os_type=redhat
    -D examples=true
    -D tests=true
    -D gtk_doc=true
    -D man=true
    -D js_engine=duktape
  )
  arch-meson polkit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -d -o root -g 102 -m 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  mv "$pkgdir"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}

# vim: ts=2 sw=2 et:
