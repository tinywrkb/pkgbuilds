# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-duktape
pkgver=0.120.r6.gb10a1bd
pkgrel=2
pkgdesc="polkit with duktape as the javascript engine"
arch=(x86_64)
license=(LGPL)
url="https://www.freedesktop.org/wiki/Software/polkit/"
depends=(glib2 pam expat systemd)
makedepends=(meson gtk-doc gobject-introspection git)
checkdepends=(python-dbusmock)
provides=(libpolkit-{agent,gobject}-1.so)
conflicts=(polkit)
provides=(polkit)
backup=(etc/pam.d/polkit-1)
_commit=b10a1bdb697045db40774f2a9a8c58ae5c7189c3  # 0.120.r6.gb10a1bd
_dukver=2.7.0
source=("git+https://gitlab.freedesktop.org/polkit/polkit.git#commit=$_commit"
        "https://duktape.org/duktape-$_dukver.tar.xz"
        '0001-duktape-static-link.patch')
sha256sums=('SKIP'
            '90f8d2fa8b5567c6899830ddef2c03f3c27960b11aca222fa17aa7ac613c2890'
            '5fdb5ba83d56ab5fb97be5c7e42bb9071021753da497f6e9ff2a99399ed7bb0a')

pkgver() {
  cd polkit
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd polkit

  # https://gitlab.freedesktop.org/polkit/polkit/-/merge_requests/99
  git fetch origin refs/merge-requests/99/head
  git cherry-pick -n a96c5119f726225f8d79b222c85d71a9f0e32419

  patch -Np1 -i ../0001-duktape-static-link.patch
}

build() {
  ln -s ../duktape-$_dukver polkit/duktape
  # build duktape
  #(
  #  cd duktape-$_dukver
  #  echo "Compiling duktape..."
  #  gcc $CFLAGS -DDUK_USE_FASTINT $CPPFLAGS -c -fPIC -Wall -Wextra -o src/duktape.o src/duktape.c
  #)
  local meson_options=(
    --prefix=/usr
    -D session_tracking=libsystemd-login
    -D os_type=redhat
    -D examples=true
    -D tests=true
    -D gtk_doc=true
    -D man=true
    -D js_engine=duktape
  )
  arch-meson polkit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -d -o root -g 102 -m 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  mv "$pkgdir"/{etc,usr/share}/polkit-1/rules.d/50-default.rules

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}

# vim: ts=2 sw=2 et:
