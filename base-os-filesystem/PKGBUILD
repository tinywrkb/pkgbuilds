# Maintainer: tinywrkb <tinywrkb@gmail.com>
#
# core/filesystem
#   Contributor: SÃ©bastien Luttringer
#   Contributor: Tom Gundersen <teg@jklm.no>

pkgname=base-os-filesystem
pkgver=2022.12.19.1
pkgrel=1
pkgdesc=""
arch=(any)
license=(GPL)
url=''
backup=(etc/{group,gshadow,passwd,shadow,shells})
# not backed-up: etc/{crypttab,fstab,host.conf,hosts,issue,ld.so.conf,nsswitch.conf,profile}
conflicts=(filesystem)
provides=(filesystem)

source=(arch-release issue os-release
        archlinux-logo{.{png,svg},-text{,-dark}.svg}
        background-{black,solarized}-1920x1080.png
        crypttab fstab
        group gshadow passwd shadow
        host.conf hosts nsswitch.conf resolv.conf
        env-generator-{system,user{,-video}} ld.so.conf locale.conf locale.sh profile shells vconsole.conf
        limits sysctl sysusers tmpfiles)
sha256sums=(${source[@]//*/SKIP})

package() {
  depends=(iana-etc)
  # TODO: report bug: timedatectl doesn't follow /etc/adjtime symlink
  # TODO: report bug: homectl create doesn't follow skel symlink
  # TODO: maybe install a default .updated to usr/etc so the systemd-update-done.service won't fail,
  #        also needs setting timestamp of /usr in the initramfs
  #        current workaround is to avoid the service by using a kernel parameter

  ################################
  #######  create hierarchy ######
  ################################

  # setup /usr hierarchy
  install -dm755 ${pkgdir}/usr/{bin,include,lib,share/{misc,pacman/db,pixmaps},src}
  install -dm755 ${pkgdir}/usr/share/man/man{1..8}

  # add lib symlinks
  [[ $CARCH = 'x86_64' ]] && {
    ln -s lib ${pkgdir}/usr/lib64
  }

  # add bin symlink
  ln -s bin ${pkgdir}/usr/sbin

  # setup etc hierarchy
  install -dm755 ${pkgdir}/etc/{issue.d,ld.so.conf.d,skel,profile.d}

  ################################
  ########  install files ########
  ################################

  # wallpapers generated with archur:
  #   $ archur -o black-1920x1080.png --text '' -r 1920x1080  -fg '#eeeeee' -bg '#101010'
  #   $ archur -o solarized-1920x1080.png --text '' -r 1920x1080  -fg '#93a1a1' -bg '#002b36'

  # TODO: install resolv.conf as a template? can somehow be used as a fallback?

  install -Dm644 -t ${pkgdir}/etc/ \
    arch-release issue \
    fstab \
    group passwd \
    host.conf hosts nsswitch.conf \
    ld.so.conf locale.conf profile shells vconsole.conf

  install -Dm600 -t ${pkgdir}/etc/ \
    crypttab \
    gshadow shadow

  install -Dm644 -t ${pkgdir}/etc/profile.d/ \
    locale.sh

  # os-release
  install -Dm644 -t ${pkgdir}/usr/lib/ \
    os-release
  # relative symlinks breaking out of /etc are bad for compatibility between folder and symlink /etc
  # this needs disabling the relevant line in systemd's tmpfiles.d/etc.conf to avoid a possible conflict
  #ln -s -t ${pkgdir}/etc/ \
  #  ../usr/lib/os-release
  install -Dm644 -t ${pkgdir}/etc/ \
    os-release

  # setup pam_limits
  install -Dm644 limits ${pkgdir}/etc/security/os.conf

  # setup systemd-sysctl
  install -Dm644 sysctl ${pkgdir}/usr/lib/sysctl.d/10-os.conf

  # setup systemd-sysusers
  install -Dm644 sysusers ${pkgdir}/usr/lib/sysusers.d/os.conf

  # setup systemd-tmpfiles
  install -Dm644 tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/os.conf

  # setup systemd.environment-generator
  install -Dm755 env-generator-system ${pkgdir}/usr/lib/systemd/system-environment-generators/10-os
  install -Dm755 env-generator-user ${pkgdir}/usr/lib/systemd/user-environment-generators/10-os
  install -Dm755 env-generator-user-video ${pkgdir}/usr/lib/systemd/user-environment-generators/12-video

  # add logo
  install -Dm644 -t ${pkgdir}/usr/share/pixmaps/ \
    archlinux-logo{.{png,svg},-text{,-dark}.svg}

  # add backgrounds
  for f in background-{black,solarized}-1920x1080.png; do
    install -Dm644 $f ${pkgdir}/usr/share/backgrounds/os/${f#background-}
  done

  # /etc/mtab
  # requirement: must be relative
  # users in a booted-up system:
  #   - fusermount/fuse, flatpak (had some code changes for systems where it doesn't exist)
  # users in a container/chroot
  #   - systemd-tmpfiles.d, systemd-nspawn
  # possible workarounds:
  #   - only bind usr/etc to etc, never use a symlink
  #   - add a second level of symlink to point to the right location
  ln -s ../proc/self/mounts ${pkgdir}/etc/mtab
  # compat for etc symlink
  mkdir -p ${pkgdir}/usr/proc/self
  echo 'compat symlink for systems where /etc is a symlink pointing to /usr/etc' \
    > ${pkgdir}/usr/proc/self/README
  ln -s ../../../proc/self/mounts  ${pkgdir}/usr/proc/self/mounts

  ################################
  ########  var symlinks #########
  ################################

  ln -s -t ${pkgdir}/etc/ \
    /var/etc/{adjtime,resolv.conf}

  # localtime symlink
  # it's time to stop usign this symlink and replaced with something
  # dynamic that doesn't depend on manually changing files in /etc
  # patching systemd to recursively reslove symlink is not a proper solution
  # as it will break applications that expect a single level of symlink (e.g. flatpak)
  # just hardcode the system timezone and use TZ environment variable for the user
  ln -s /usr/share/zoneinfo/UTC ${pkgdir}/etc/localtime

  # machine specific identifiers
  # TODO: replace systemd's utils
  # mount points for compatibility with systemd's utils
  #touch {,usr/}etc/{hostname,machine-id,machine-info}
  ln -s -t ${pkgdir}/etc/ \
    /var/etc/{hostname,machine-id,machine-info}
}
