# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo-minimal
pkgver=21.09
pkgrel=1
pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')
depends=(curl glib2 libmms libzen tinyxml2)
conflicts=(libmediainfo)
source=("https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz"
        'fix_unresolved_symbols.patch::https://patch-diff.githubusercontent.com/raw/MediaArea/MediaInfoLib/pull/1434.patch')
sha512sums=('a0b5ea9c6fce3f5551176d411b8c227927f579c3f09442316f6c380926acc545ea346e044f808b66403b1f0aac6f88ed4413c8231e792256d3cd7676133ef3be'
            'c374e1307ffb95ea9c23f803ddcd9be55a47600a60491a71f367c07c178225deaa83165b14f7eb2b2a6d3e5ebc923f527fec1f018537b9504943a55a29a79845')

prepare() {
  cd MediaInfoLib
  patch -p1 -i ../fix_unresolved_symbols.patch
}

build() {
  cd MediaInfoLib/Project/GNU/Library
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2
  make
}

package() {
  make -C MediaInfoLib/Project/GNU/Library DESTDIR="$pkgdir" install
  install -Dm644 MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
