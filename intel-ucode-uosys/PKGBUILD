# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=intel-ucode-uosys
pkgver=20210216
pkgrel=1
pkgdesc='Microcode update files for Intel CPUs'
arch=('any')
url='https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files'
conflicts=('intel-ucode')
provides=('intel-ucode')
replaces=('microcode_ctl')
makedepends=('iucode-tool')
license=('custom')
source=("${pkgname}-${pkgver/./}.tar.gz::https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/archive/microcode-${pkgver/./}.tar.gz")
sha256sums=('b855c81f78705f35341248a0603aa1a6e199ca7f59cd425e061b579329aa9eaa')

build() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  rm -f intel-ucode{,-with-caveats}/list
  mkdir -p kernel/x86/microcode
  iucode_tool --write-earlyfw=intel-ucode.img intel-ucode{,-with-caveats}/
}

package() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  install -D -m0644 intel-ucode.img "${pkgdir}"/usr/lib/firmware/intel-ucode/intel-ucode.img
  install -D -m0644 license "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
