# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@seblu.net>

pkgname=qemu-headless-combined
pkgdesc="A generic and open source machine emulator and virtualizer"
pkgver=5.2.0
pkgrel=5
arch=(x86_64)
url="https://wiki.qemu.org/"
license=(GPL2 LGPL2.1)
install=qemu-headless-combined.install
depends=(seabios gnutls libpng libaio numactl libnfs
               lzo snappy curl vde2 libcap-ng spice libcacard usbredir libslirp
               libssh zstd liburing virglrenderer ndctl dtc)
makedepends=(spice-protocol python python-sphinx ninja)
optdepends=('samba: SMB/CIFS server support')
conflicts=(qemu qemu-headless qemu-arch-extra qemu-headless-arch-extra)
provides=(qemu qemu-headless qemu-arch-extra qemu-headless-arch-extra)
options=(!strip !emptydirs)
source=(https://download.qemu.org/qemu-$pkgver.tar.xz
        65-kvm.rules)
sha512sums=('bddd633ce111471ebc651e03080251515178808556b49a308a724909e55dac0be0cc0c79c536ac12d239678ae94c60100dc124be9b9d9538340c03a2f27177f3'
            'bdf05f99407491e27a03aaf845b7cc8acfa2e0e59968236f10ffc905e5e3d5e8569df496fd71c887da2b5b8d1902494520c7da2d3a8258f7fd93a881dd610c99')

prepare() {
  cd qemu-${pkgver}

  sed -i 's/\(#if SPICE_SERVER_VERSION >=\) 0x000e04/\1 0x000e05/' ui/spice-display.c
}

build() (
  cd qemu-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/qemu \
    --extra-ldflags="$LDFLAGS" \
    --smbd=/usr/bin/smbd \
    --enable-modules \
    --enable-slirp=system \
    --audio-drv-list= \
    --disable-xfsctl \
    --disable-sdl \
    --disable-gtk \
    --disable-vte \
    --disable-brlapi \
    --enable-opengl \
    --enable-virglrenderer \
    --target-list=aarch64-softmmu,arm-softmmu,avr-softmmu,m68k-softmmu,x86_64-softmmu,aarch64_be-linux-user,aarch64-linux-user,armeb-linux-user,arm-linux-user,m68k-linux-user,x86_64-linux-user
   #--audio-drv-list="pa alsa sdl jack"

  ninja -C build
)


package() {
  cd qemu-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install

  # systemd stuff
  install -Dm644 "$srcdir"/65-kvm.rules "$pkgdir/usr/lib/udev/rules.d/65-kvm.rules"

  # remove conflicting /var/run directory
  cd "$pkgdir"
  rm -r var

  cd usr/lib

  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s qemu/qemu-bridge-helper

  cd ../bin
  for _bin in qemu-*; do
    [[ -f $_bin ]] || continue

    case ${_bin#qemu-} in
      # guest agent
      ga) rm "$_bin"; continue ;;

      # tools
      edid|img|io|keymap|nbd|pr-helper|storage-daemon) continue ;;

      # core emu
      system-${_corearch}) continue ;;
    esac

  done

  cd ../share/qemu
  for _blob in *; do
    [[ -f $_blob ]] || continue

    case $_blob in
      # provided by seabios package
      bios.bin|bios-256k.bin|vgabios-cirrus.bin|vgabios-qxl.bin|\
      vgabios-stdvga.bin|vgabios-vmware.bin|vgabios-virtio.bin|vgabios-bochs-display.bin|\
      vgabios-ramfb.bin) rm "$_blob"; continue ;;

      # provided by edk2-ovmf package
      edk2-*) rm "$_blob"; continue ;;

      # iPXE ROMs
      efi-*|pxe-*) continue ;;

      # core blobs
      bios-microvm.bin|kvmvapic.bin|linuxboot*|multiboot.bin|sgabios.bin|vgabios*) continue ;;

      # Trace events definitions
      trace-events*) continue ;;
    esac

  done

  # provided by edk2-ovmf package
  rm -r firmware

  cd ..
  rm -r {applications,icons}
}

# vim:set ts=2 sw=2 et:
