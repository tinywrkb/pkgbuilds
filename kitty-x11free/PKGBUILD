# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix
# Contributor: Maximilian Kindshofer <maximilian@kindshofer.net>

_pkgname=kitty
pkgname=$_pkgname-x11free
pkgver=0.20.2
pkgrel=1
pkgdesc="A modern, hackable, featureful, OpenGL-based terminal emulator"
arch=('x86_64')
url="https://github.com/kovidgoyal/kitty"
license=('GPL3')
depends=('python' 'freetype2'  'fontconfig' 'wayland' 'libxkbcommon'
         'hicolor-icon-theme' 'libgl' 'dbus' 'lcms2')
makedepends=('wayland-protocols' 'python-sphinx')
conflicts=($_pkgname)
provides=($_pkgname)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/kovidgoyal/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('3cee1501c2787283654e0c48a6dd40a969d570003722dc0558d278b29a22df51d4e215d66cd74a417a08bff9beaf8cf69f594f34dab23647d9abdba831c9fb65')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's/x11 wayland/wayland/' setup.py
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py linux-package --update-check-interval=0
}

package() {
  depends+=('kitty-terminfo')
  optdepends=('imagemagick: viewing images with icat')

  cd "$srcdir/$_pkgname-$pkgver"

  cp -r linux-package "${pkgdir}"/usr

  # completions
  python __main__.py + complete setup bash | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/kitty
  python __main__.py + complete setup fish | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/fish/vendor_completions.d/kitty.fish
  # doesn't know how to http://zsh.sourceforge.net/Doc/Release/Completion-System.html#Autoloaded-files
  # so we write our own header
  {
      echo "#compdef kitty"
      python __main__.py + complete setup zsh
  } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_kitty

  install -Dm644 "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/kitty.png "${pkgdir}"/usr/share/pixmaps/kitty.png

  rm -r "$pkgdir"/usr/share/terminfo

  install -Dm644 docs/generated/conf/kitty.conf "${pkgdir}"/usr/share/doc/${pkgname}/kitty.conf
}
