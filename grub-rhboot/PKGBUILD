# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# WARNING: no arch linux detection script fixed are supplied with this package

pkgname=grub-rhboot
pkgdesc="GNU GRand Unified Bootloader (2)"
epoch=2
pkgver=2.06.r202.g8c68222cb
pkgrel=1
url="https://github.com/rhboot/grub2"
arch=(x86_64)
license=(GPL3)
options=('!makeflags')
depends=(device-mapper gettext)
makedepends=(autogen freetype2 fuse2 git help2man python rsync texinfo ttf-dejavu)
conflicts=(grub)
provides=(grub)
optdepends=('dosfstools: For grub-mkrescue FAT FS and EFI support'
            'efibootmgr: For grub-install EFI support'
            'freetype2: For grub-mkfont usage'
            'fuse2: For grub-mount usage'
            'libisoburn: Provides xorriso for generating grub rescue iso using grub-mkrescue'
            'lzop: For grub-mkrescue LZO support'
            'mtools: For grub-mkrescue FAT FS support'
            'os-prober: To detect other OSes when generating grub.cfg in BIOS systems')
# _stablever is set in the package() function
# check that it's still correct when switching branches
#_stablever=2.06
# https://src.fedoraproject.org/rpms/grub2/tree/f36
_commit=8c68222cbb8e7527c447bd6908578b72b7302f80  # branch/fedora36
_gnulib_commit=6db02d3d2febe5e6e855d94722bb24f097c80a6f  # branch/fixes
# in f36 unifont is http://unifoundry.com/pub/unifont/unifont-13.0.06/font-builds/unifont-13.0.06.pcf.gz
_unifont_ver=14.0.01
source=("rhboot-grub::git+https://github.com/rhboot/grub2.git#commit=${_commit}"
        "rhboot-gnulib::git+https://github.com/rhboot/gnulib.git#commit=${_gnulib_commit}"
        "https://ftp.gnu.org/gnu/unifont/unifont-${_unifont_ver}/unifont-${_unifont_ver}.bdf.gz")

sha256sums=('SKIP'
            'SKIP'
            '391d194f6307fcd0915daafd360509a734e26f3e4013e63d47deb2530d59e66e')

pkgver() {
  cd rhboot-grub
  git describe --long --tags 2>/dev/null | sed 's/^grub-//;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd rhboot-grub

  echo "Fix DejaVuSans.ttf location so that grub-mkfont can create *.pf2 files for starfield theme..."
  sed 's|/usr/share/fonts/dejavu|/usr/share/fonts/dejavu /usr/share/fonts/TTF|g' -i "configure.ac"

  echo "Avoid problem with unifont during compile of grub..."
  # http://savannah.gnu.org/bugs/?40330 and https://bugs.archlinux.org/task/37847
  gzip -cd ../unifont-${_unifont_ver}.bdf.gz > unifont.bdf

  echo "Make translations reproducible..."
  sed -i '1i /^PO-Revision-Date:/ d' po/*.sed
}

build() {
  (
    echo "Run bootstrap..."
    cd rhboot-grub
    ./bootstrap --no-git --gnulib-srcdir=../rhboot-gnulib
  )

  echo "Unset all compiler FLAGS..."
  unset CFLAGS
  unset CPPFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  # TODO: find the cause and drop this
  export HOST_CPPFLAGS=-I/usr/include

  local common_configure_options=(
    FREETYPE="pkg-config freetype2"
    BUILD_FREETYPE="pkg-config freetype2"
    --bindir=/usr/bin
    --datarootdir=/usr/share
    --infodir=/usr/share/info
    --mandir=/usr/share/man
    --prefix=/usr
    --sbindir=/usr/bin
    --sysconfdir=/etc
    --enable-mm-debug
    --enable-nls
    --enable-device-mapper
    --enable-cache-stats
    --enable-grub-mkfont
    --enable-grub-mount
    --program-prefix=""
    --with-bootdir=/boot
    --with-grubdir=grub
    --disable-silent-rules
    --disable-werror
  )

  (
    echo "Build grub bios stuff..."
    echo "Set ARCH dependent variables for bios build..."

    echo "Copy the source for building the bios part..."
    cp -r rhboot-grub build-bios
    cd build-bios

    echo "Run ./configure for bios build..."
    ./configure \
      --with-platform=pc \
      --target=i386 \
      --enable-efiemu \
      --enable-boot-time \
      "${common_configure_options[@]}"

    if [ ! -z "${SOURCE_DATE_EPOCH}" ]; then
      echo "Make info pages reproducible..."
      touch -d "@${SOURCE_DATE_EPOCH}" $(find -name '*.texi')
    fi

    echo "Run make for bios build..."
    make
  )

  (
    echo "Build grub x86_64 efi stuff..."
    echo "Copy the source for building the x86_64 efi part..."
    cp -r rhboot-grub build-efi-x86_64
    cd build-efi-x86_64

    echo "Run ./configure for x86_64 efi build..."
    ./configure \
      --with-platform=efi \
      --target=x86_64 \
      --disable-efiemu \
      --enable-boot-time \
      "${common_configure_options[@]}"

    echo "Run make for x86_64 efi build..."
    make
  )
}

package() {
  _stablever=$(echo $pkgver | sed 's/\([[:digit:].]\+\)\.r.*/\1/')
  echo "grub stable version is $_stablever"
  (
    echo "Package grub x86_64 efi stuff..."
    cd build-efi-x86_64

    echo "Run make install for x86_64 efi build..."
    make DESTDIR="${pkgdir}" bashcompletiondir="/usr/share/bash-completion/completions" install

    _sbat_file="${pkgdir}"/usr/share/grub/sbat.csv
    touch "${_sbat_file}"
    echo "sbat,1,SBAT Version,sbat,1,https://github.com/rhboot/shim/blob/main/SBAT.md" >> "${_sbat_file}"
    echo "grub,1,Free Software Foundation,grub,${_stablever},https//www.gnu.org/software/grub/" >> "${_sbat_file}"
    echo "grub.arch,1,Arch Linux,grub,${_stablever},https://archlinux.org/packages/core/x86_64/grub/" >> "${_sbat_file}"
  )

  (
    echo "Package grub bios stuff..."
    cd build-bios

    echo "Run make install for bios build..."
    make DESTDIR="${pkgdir}" bashcompletiondir="/usr/share/bash-completion/completions" install
  )

  echo "Remove gdb debugging related files..."
  rm -f "${pkgdir}"/usr/lib/grub/*/*.module || true
  rm -f "${pkgdir}"/usr/lib/grub/*/*.image || true
  rm -f "${pkgdir}"/usr/lib/grub/*/{kernel.exec,gdb_grub,gmodule.pl} || true
}
