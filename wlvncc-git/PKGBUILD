# Maintainer: tinywrkb <tinywrkb@gmail.com>

_gitname=wlvncc
pkgname=$_gitname-git
pkgver=r31.67bd333
pkgrel=1
pkgdesc='a native wayland vnc client'
url='https://github.com/any1/wlvncc'
license=('custom:ISC')
arch=(x86_64)
depends=(aml libvncserver libxkbcommon pixman wayland)
makedepends=(git meson wayland-protocols)
conflicts=($_gitname)
provides=($_gitname)
_amlcommit=bc9f6b84f9030730d44700896fe0751a2a545e1b  # tags/v0.1.0
source=("$_gitname::git+${url}.git"
        "aml::git+https://github.com/any1/aml.git#commit=$_amlcommit")
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd $_gitname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $_gitname
  mkdir -p subprojects
  ln -s ../../aml subprojects/aml
}

build() {
  arch-meson $_gitname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $_gitname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
  cd "$pkgdir"/usr
  rm include/aml.h lib/{pkgconfig/aml.pc,libaml.so*}
  rmdir include lib{/pkgconfig,}
}
