# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=libblockdev
pkgname=$_pkgname-minimal
pkgver=2.26
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'dosfstools' 'gptfdisk' 'libbytesize' 'mdadm' 'ndctl'
         'parted' 'volume_key' 'kmod' 'libyaml')
makedepends=('autoconf-archive' 'gobject-introspection' 'systemd' 'python')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('662f9b6a03933a7192079dd2826f87e8465a5a2d792c8768a4cb241cc8adecf04d7558a082bec7cd83ac9cddb3121362ce9cd5eecc73a91ac3ea517e5911a7ed')

build() {
  cd "$srcdir"/$_pkgname-$pkgver-1
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --without-dmraid \
    --without-lvm \
    --without-lvm_dbus \
    --without-vdo \

  make
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver-1
  make check
}

package() {
  conflicts+=($_pkgname)
  provides+=($_pkgname)
  cd "$srcdir"/$_pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
