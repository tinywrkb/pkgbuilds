# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=appstream-glib
pkgname=${_pkgname}-gtkfree
pkgver=0.7.18
pkgrel=1
pkgdesc="Objects and methods for reading and writing AppStream metadata"
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
arch=(x86_64)
license=(LGPL)
depends=(libyaml pacman gcab libsoup libstemmer json-glib gdk-pixbuf2)
makedepends=(gobject-introspection git gperf meson)
provides=(appdata-tools libappstream-glib.so)
provides+=(appstream-glib)
conflicts=(appdata-tools)
conflicts+=(appstream-glib)
replaces=(appdata-tools)
_commit=705e8e2061e9c9ef741792b1de56c6529fb57082  # tags/appstream_glib_0_7_18^0
source=("git+https://github.com/hughsie/appstream-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^appstream_glib_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $_pkgname
}

build() {
  arch-meson $_pkgname build \
    -D builder=false \
    -D fonts=false \
    -D alpm=true \
    -D gtk-doc=false \
    -D rpm=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
