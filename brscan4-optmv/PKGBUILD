# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Harvey <harv@gmx.de>

license=('GPL' 'custom:Brother')
arch=('i686' 'x86_64')
_pkgname=brscan4
pkgname=${_pkgname}-optmv
pkgver=0.4.9_1
pkgrel=1
pkgdesc="SANE drivers from Brother for brscan4 compatible models"
backup=('etc/opt/brother/scanner/brscan4/brsanenetdevice4.cfg')
depends=('sane' 'libusb-compat')
conflicts=('brscan4')
provides=('brscan4')
url="http://support.brother.com"
install=brscan4.install

[ "$CARCH" = "x86_64" ] && pkg="dlf006648/${_pkgname}-${pkgver/_/-}.x86_64.rpm" || pkg="dlf006647/${_pkgname}-${pkgver/_/-}.i386.rpm"
[ "$CARCH" = "x86_64" ] && pkg_md5sum="02329240d6f8943f8c9e8dd663878a1d" || pkg_md5sum="ee9abd2579b107ae13de3fb54cd82919"

source=("https://download.brother.com/welcome/$pkg"
	"agree.html"
	mk-udev-rules)
md5sums=($pkg_md5sum
	 'ccffb9a6f6d436b21be25b0241068981'
	 '9a23d2af36609fbf2b0aa75269553701')

build() {
  cd "$srcdir"
  umask 022
  mkdir -p usr/lib64/udev/rules.d
  ./mk-udev-rules opt/brother/scanner/brscan4/{Brsane4.ini,models4/*.ini} > usr/lib64/udev/rules.d/40-$_pkgname.rules
}

package() {
  cp -r $srcdir/etc $pkgdir
  cp -r $srcdir/usr $pkgdir
  cp -r $srcdir/opt $pkgdir/usr
  install -D -m644 $srcdir/agree.html $pkgdir/usr/share/licenses/$_pkgname/LICENSE.html
  [ "$CARCH" = "x86_64" ] && mv $pkgdir/usr/lib64 $pkgdir/usr/lib
  # move the links to the right location
  cd $pkgdir/usr/lib/sane
  ln -sf libsane-brother4.so.1.0.7 $pkgdir/usr/lib/sane/libsane-brother4.so.1
  ln -sf libsane-brother4.so.1 $pkgdir/usr/lib/sane/libsane-brother4.so
  # fix symlinks after the /opt move
  ln -sfT /usr/opt/brother/scanner/brscan4/models4 $pkgdir/etc/opt/brother/scanner/brscan4/models4
  ln -sf /usr/opt/brother/scanner/brscan4/Brsane4.ini $pkgdir/etc/opt/brother/scanner/brscan4/Brsane4.ini
  #ln -sf /usr/opt/brother/scanner/brscan4/brsanenetdevice4.cfg $pkgdir/etc/opt/brother/scanner/brscan4/brsanenetdevice4.cfg
  rm $pkgdir/etc/opt/brother/scanner/brscan4/brsanenetdevice4.cfg
  install -Dm644 $pkgdir/usr/opt/brother/scanner/brscan4/brsanenetdevice4.cfg $pkgdir/etc/opt/brother/scanner/brscan4/brsanenetdevice4.cfg
  ln -sf /usr/opt/brother/scanner/brscan4/brsaneconfig4 $pkgdir/usr/bin/brsaneconfig4
}
