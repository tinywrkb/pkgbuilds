# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

_pkgname=igt-gpu-tools
pkgname=intel-gpu-tools-x11free
pkgver=1.26
pkgrel=1
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(x86_64)
license=(MIT)
url='https://gitlab.freedesktop.org/drm/igt-gpu-tools'
depends=(libdrm libpciaccess cairo python procps-ng kmod libunwind peg systemd)
makedepends=(swig gtk-doc meson)
source=(https://xorg.freedesktop.org/releases/individual/app/${_pkgname}-$pkgver.tar.xz{,.sig})
sha512sums=('6eb67f68da1c79fac61e5abd57433ef51dbeb2b6f0923dc84aefb81d7059fbc94378fadcd7321a8f059edd9bac640032bf53c3aa058cf570fe5c31924396af8f'
            'SKIP')
validpgpkeys=('775965B85650195ACE77E18D7370055DB74C2475'
              'E1996E8A119F43C4BA2BE889D8C620766EA3F69D')

prepare() {
  mkdir -p build
}

build() {
  cd build
  meson ../$_pkgname-$pkgver \
    -D overlay=disabled \
    --prefix=/usr \
    --libexecdir=/usr/lib

  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  cd ../$_pkgname-$pkgver
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
