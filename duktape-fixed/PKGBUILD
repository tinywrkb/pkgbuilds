# Maintainer tinywrkb <tinywrkb@gmail.com>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape-fixed
pkgver=2.7.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=(x86_64)
url='https://duktape.org/'
license=(MIT)
depends=(glibc)
conflicts=(duktape)
provides=(duktape)
source=("https://duktape.org/duktape-$pkgver.tar.xz")
sha256sums=('90f8d2fa8b5567c6899830ddef2c03f3c27960b11aca222fa17aa7ac613c2890')

prepare() {
  cd duktape-$pkgver

  mv Makefile.sharedlibrary Makefile

  # use our default optimization flag
  sed '/$(CC)/ s/-Os//' -i Makefile

  # force linking against libm
  sed 's@\($(DUKTAPE_SRCDIR)/duktape.c\)@\1 -lm@' -i Makefile
}

build() {
  cd duktape-$pkgver
  CFLAGS+=" -DDUK_USE_FASTINT" make INSTALL_PREFIX=/usr
}

package() {
  cd duktape-$pkgver
  make DESTDIR="$pkgdir" INSTALL_PREFIX=/usr install
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
