# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

pkgname=xdg-desktop-portal-gtk-x11free
pkgver=1.12.0
pkgrel=1
pkgdesc="A backend implementation for xdg-desktop-portal using GTK"
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
arch=(x86_64)
license=(LGPL)
depends=(gtk3-nonsensefree gsettings-desktop-schemas)
makedepends=(xdg-desktop-portal python git)
optdepends=("evince: Print preview")
conflicts=(xdg-desktop-portal-gtk)
provides=(xdg-desktop-portal-impl xdg-desktop-portal-gtk)
_commit=2ea8803d97ec4e0bb9d51b35c6ae522d27d6a79a  # tags/1.12.0^0
source=("git+https://github.com/flatpak/xdg-desktop-portal-gtk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd xdg-desktop-portal-gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd xdg-desktop-portal-gtk
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd xdg-desktop-portal-gtk
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make 
}

check() {
  cd xdg-desktop-portal-gtk
  make check
}

package() {
  cd xdg-desktop-portal-gtk
  DESTDIR="$pkgdir" make install
}
