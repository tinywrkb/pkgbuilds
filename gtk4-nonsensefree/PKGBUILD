# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gtk4-nonsensefree
pkgver=4.4.0
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(glib2 json-glib rest
         cairo gdk-pixbuf2 graphene libepoxy libgl libegl
         fribidi fontconfig harfbuzz pango
         libcolord dconf libcups librsvg libxkbcommon wayland
         desktop-file-utils
         adwaita-icon-theme cantarell-fonts iso-codes shared-mime-info)
makedepends=(git meson docbook-xsl wayland-protocols sysprof python
             shaderc sassc gobject-introspection)
provides=(gtk4)
conflicts=(gtk4)
license=(LGPL)
_commit=f1f197e3b94a55d5cbfaae2498f991a0ae733b32  # tags/4.4.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
        gtk4-querymodules.hook)
sha256sums=('SKIP'
            'cd8e607eddd9941f279084e1d15309941423d26cca1897f43524a02e58e48816')

pkgver() {
  cd gtk
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  # https://gitlab.gnome.org/GNOME/gtk/-/commit/df4b564d69cc7d2e751537eff61259b36f37e9e5
  CFLAGS+=" -DG_ENABLE_DEBUG -DG_DISABLE_CAST_CHECKS"
  # TODO: set build type? --buildtype release
  arch-meson gtk build \
    -D x11-backend=false \
    -D wayland-backend=true \
    -D broadway_backend=false \
    -D media-ffmpeg=disabled \
    -D media-gstreamer=disabled \
    -D print-cups=auto \
    -D cloudproviders=disabled \
    -D sysprof=enabled \
    -D tracker=disabled \
    -D colord=enabled \
    -D gtk_doc=false \
    -D man-pages=true \
    -D introspection=auto \
    -D demos=false \
    -D build-examples=false \
    -D build-tests=false \

  meson compile -C build
  ninja -C build
}

package() {
  depends+=(gtk-update-icon-cache)
  provides+=(libgtk-4.so)

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-4.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk4-querymodules.hook

  cd "$pkgdir"
  rm usr/bin/gtk4-update-icon-cache
  rm usr/share/man/man1/gtk4-update-icon-cache.1
  rm usr/share/man/man1/gtk4-broadwayd.1
}

# vim:set ts=2 sw=2 et:
