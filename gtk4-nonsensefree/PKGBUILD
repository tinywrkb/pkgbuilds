# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gtk4-nonsensefree
pkgver=4.4.1
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(glib2 cairo pango fribidi gdk-pixbuf2 libepoxy libgl libegl harfbuzz
         libxkbcommon graphene iso-codes wayland libcolord
         fontconfig libcups rest json-glib
         librsvg dconf shared-mime-info desktop-file-utils
         adwaita-icon-theme cantarell-fonts)
makedepends=(git meson shaderc sassc gobject-introspection docbook-xsl
             wayland-protocols)
provides=(gtk4)
conflicts=(gtk4)
license=(LGPL)
_commit=3090fbac82d86543cd198ddf38d4044e05c87d91  # tags/4.4.1^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
        gtk4-querymodules.hook)
sha256sums=('SKIP'
            'cd8e607eddd9941f279084e1d15309941423d26cca1897f43524a02e58e48816')

pkgver() {
  cd gtk
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  # TODO: set build type? --buildtype release
  local meson_options=(
    # GDK backends
    -D x11-backend=false
    -D wayland-backend=true
    -D broadway-backend=false
    # Media backends
    -D media-ffmpeg=disabled
    -D media-gstreamer=disabled
    # Print backends
    -D print-cups=auto
    # Optional features
    -D cloudproviders=disabled
    -D sysprof=disabled
    -D tracker=disabled
    -D colord=enabled
    # Documentation and introspection
    -D gtk_doc=false
    -D man-pages=true
    -D introspection=auto
    # Demos, examples and tests
    -D demos=false
    -D build-examples=false
    -D build-tests=false
  )
  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
  ninja -C build
}

package() {
  depends+=(gtk-update-icon-cache)
  provides+=(libgtk-4.so)

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-4.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk4-querymodules.hook

  cd "$pkgdir"
  rm usr/bin/gtk4-update-icon-cache
  rm usr/share/man/man1/gtk4-update-icon-cache.1
  rm usr/share/man/man1/gtk4-broadwayd.1
}

# vim:set ts=2 sw=2 et:
