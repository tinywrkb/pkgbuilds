# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gtk4-nonsensefree
pkgver=4.0.3
pkgrel=1
epoch=1
pkgdesc="GObject-based multi-platform GUI toolkit"
arch=(x86_64)
url="https://www.gtk.org/"
depends=(glib2 json-glib rest
         cairo gdk-pixbuf2 graphene libepoxy libgl libegl vulkan-icd-loader
         fribidi fontconfig harfbuzz pango
         colord dconf libcups librsvg libxkbcommon wayland
         desktop-file-utils
         adwaita-icon-theme cantarell-fonts iso-codes shared-mime-info)
makedepends=(git meson docbook-xsl wayland-protocols sysprof python
             shaderc sassc gobject-introspection vulkan-headers)
license=(LGPL)
_commit=463794e9395a7e5701559a67a2072e18bbfacf06  # tags/4.0.3^0
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
        gtk4-querymodules.hook)
sha256sums=('SKIP'
            'cd8e607eddd9941f279084e1d15309941423d26cca1897f43524a02e58e48816')

pkgver() {
  cd gtk
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk
}

build() {
  CFLAGS+=" -DG_ENABLE_DEBUG -DG_DISABLE_CAST_CHECKS"
  arch-meson gtk build \
    -D x11-backend=false \
    -D broadway_backend=false \
    -D media-ffmpeg=disabled \
    -D media-gstreamer=disabled \
    -D print-cloudprint=disabled \
    -D xinerama=disabled \
    -D cloudproviders=disabled \
    -D sysprof=enabled \
    -D tracker=disabled \
    -D colord=enabled \
    -D gtk_doc=false \
    -D man-pages=true \
    -D demos=false \
    -D build-examples=false \
    -D build-tests=false
  meson compile -C build
  ninja -C build
}

package() {
  depends+=(gtk-update-icon-cache)
  provides+=(libgtk-4.so)

  DESTDIR="$pkgdir" meson install -C build

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-4.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk4-querymodules.hook

  # gtk-update-icon-cache is also used by other toolkits
  rm \
    $pkgdir/usr/bin/gtk4-update-icon-cache \
    $pkgdir/usr/share/man/man1/gtk4-update-icon-cache.1
}

# vim:set ts=2 sw=2 et:
