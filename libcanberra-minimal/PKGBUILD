# Maintainer: Jan de Groot <jgc@archlinux.org>

# TODO: consider dropping and package and disabling in gtk3,
#       according to the gnome gtk4 port it's deprecated.
#       https://gitlab.gnome.org/GNOME/Initiatives/-/issues/26

_pkgname=libcanberra
pkgname=${_pkgname}-minimal
pkgver=0.30+2+gc0620e4
pkgrel=5
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
url="http://0pointer.de/lennart/projects/libcanberra"
arch=(x86_64)
license=(LGPL)
depends=(libltdl libpulse libvorbis sound-theme-freedesktop tdb)
makedepends=(git gtk3)
provides=("libcanberra-pulse=$pkgver-$pkgrel" libcanberra-gtk3.so)
replaces=("libcanberra-pulse<0.30+2+gc0620e4-4")
# TODO: figure out if the libtools files of libcanberra are actually needed
options=(libtool)
_commit=c0620e432650e81062c1967cc669829dbd29b310  # master
source=("git+http://git.0pointer.net/clone/libcanberra.git#commit=$_commit"
        40-libcanberra-gtk-module.sh
        libcanberra-multi-backend.patch)
sha256sums=('SKIP'
            'a0d0b135d3fea5c703a5f84208b79d66f671b082ae85f67b629ee2568a7ddc30'
            'de146cae3e40a16b38c8edb4f1a3a423c64eb9c5000e36c316b677e9909c9b06')

pkgver() {
  cd libcanberra
  git describe --tags | sed 's/^v//;s/-/+/g'
}

_ver=${pkgver:0:4}

prepare() {
  cd libcanberra

  # https://bugs.archlinux.org/task/71341
  # https://bugs.freedesktop.org/show_bug.cgi?id=51662
  patch -Np1 -i ../libcanberra-multi-backend.patch

  # disable gtk_doc
  sed -i '/^GTK_DOC_CHECK.*/d' configure.ac

  ./autogen.sh
}

build() {
  cd libcanberra
  local config_options=(
    --localstatedir=/var
    --prefix=/usr
    --sysconfdir=/etc
    --with-systemdsystemunitdir=/usr/lib/systemd/system
    --disable-alsa
    --disable-gstreamer
    --disable-gtk
    --disable-gtk-doc
    --disable-lynx
    --disable-oss
    --disable-static
    --enable-gtk3
    --enable-null
    --enable-pulse
    --enable-tdb
    --with-builtin=dso
  )
  ./configure "${config_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  # TODO: consider adding gtk3 to depends array here
  provides+=($_pkgname)
  conflicts+=($_pkgname)

  cd libcanberra

  make -j1 DESTDIR="$pkgdir" install

  # TODO: figure out if the libtools files of libcanberra are actually needed
  rm "$pkgdir"/usr/lib/*.la
  rm "$pkgdir"/usr/lib/gtk-3.0/modules/*.la
  #rm "$pkgdir"/usr/lib/libcanberra-${_ver}/*.la

  # FS#52370: Remove login, logout and system-ready sounds.
  # We have no sound themes that include these so do not play them.
  rm -r "$pkgdir"/usr/share/{gnome,gdm}

  install -Dt "$pkgdir/etc/X11/xinit/xinitrc.d" ../40-libcanberra-gtk-module.sh
}

# vim:set sw=2 et:
