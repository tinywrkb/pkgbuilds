# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Previous maintainer: Roman Zimbelmann <romanz@lavabit.com>

pkgname=ranger-sixel
pkgver=1.9.3
pkgrel=4
pkgdesc='Simple, vim-like file manager'
arch=('any')
url='https://ranger.github.io'
license=('GPL')
depends=('python')
optdepends=(
    'atool: for previews of archives'
    'elinks: for previews of html pages'
    'ffmpegthumbnailer: for video previews'
    'highlight: for syntax highlighting of code'
    'libcaca: for ASCII-art image previews'
    'lynx: for previews of html pages'
    'mediainfo: for viewing information about media files'
    'odt2txt: for OpenDocument texts'
    'perl-image-exiftool: for viewing information about media files'
    'poppler: for pdf previews'
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'transmission-cli: for viewing bittorrent information'
    'ueberzug: for previews of images'
    'w3m: for previews of images and html pages')
conflicts=('ranger')
provides=('ranger')
checkdepends=('python-pytest')
source=(https://ranger.github.io/ranger-${pkgver}.tar.gz{,.sig}
        '0001-implement-sixel-image-previewer.patch')
sha512sums=('5cd7bea714a6542d0a5445f5f0cf929b2df4377df3ef710f3786c6c46dd58b017a8060cde3969076637bfaac03c13297d3020fd82f4574dc49546ca223c0e8d3'
            'SKIP'
            '73c009f219a8b152c50497e15c77b3663bf895401260257e59c3929ab68a96cd89e1a120b065a741a50393e5ba1f8184fb615ff8de2732308613cfcc40214c98')
validpgpkeys=('1E9B36EC051FF6F7FFC969A7F08CE1E200FB5CDF'
              '66FA95C0F1619BDA520A41F60D63346A5D15D055') # huterich

prepare() {
  cd ranger-${pkgver}
  # https://github.com/ranger/ranger/pull/2466
  patch -Np1 -i ../0001-implement-sixel-image-previewer.patch
}

check() {
  cd ranger-${pkgver}
  make test_pytest
}

package() {
  cd ranger-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
