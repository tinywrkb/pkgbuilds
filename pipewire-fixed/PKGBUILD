# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbase=pipewire
_pkgpostfix=fixed
pkgbase=$_pkgbase-$_pkgpostfix
pkgname=({pipewire,alsa-card-profiles,pipewire-docs,pipewire-alsa,pipewire-jack}-$_pkgpostfix
         {pipewire-pulse,gst-plugin-pipewire,pipewire-zeroconf,pipewire-v4l2}-$_pkgpostfix)
pkgver=0.3.40
pkgrel=1
epoch=1
pkgdesc="Low-latency audio/video router and processor"
url="https://pipewire.org"
license=(MIT)
arch=(x86_64)
makedepends=(git meson doxygen graphviz python-docutils valgrind libpulse
             alsa-lib libusb gst-plugins-base rtkit dbus sdl2 ncurses libsndfile
             bluez-libs sbc libldac libfreeaptx libfdk-aac avahi
             webrtc-audio-processing)
checkdepends=(desktop-file-utils)
_commit=7afd80052b7c49754a13c9ab49c368f95b60e0a7  # tags/0.3.40
source=("git+https://gitlab.freedesktop.org/pipewire/pipewire.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pipewire
  git describe --tags | sed 's/-/+/g'
}

build() {
  local meson_options=(
    -D bluez5-backend-hsphfpd=disabled
    -D bluez5-backend-ofono=disabled
    -D docs=enabled
    -D jack-devel=true
    -D jack=disabled
    -D libcamera=disabled
    -D libjack-path=/usr/lib
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D roc=disabled
    -D session-managers=[]
  )
  arch-meson pipewire build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_pipewire-fixed() {
  license+=(LGPL)  # libspa-alsa
  depends=(rtkit alsa-card-profiles libdbus-1.so libncursesw.so libsndfile.so
           libudev.so libusb-1.0.so libasound.so libsystemd.so libbluetooth.so
           libsbc.so libldacBT_enc.so libfreeaptx.so libfdk-aac.so
           libwebrtc_audio_processing.so)
  optdepends=('pipewire-docs-fixed: Documentation'
              'pipewire-session-manager: Session manager'
              'pipewire-alsa-fixed: ALSA configuration'
              'pipewire-jack-fixed: JACK support'
              'pipewire-pulse-fixed: PulseAudio replacement'
              'gst-plugin-pipewire-fixed: GStreamer plugin'
              'pipewire-zeroconf-fixed: Zeroconf support'
              'pipewire-v4l2-fixed: V4L2 interceptor')
  provides=(libpipewire-$_ver.so)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  install=pipewire.install

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"

    _pick acp usr/lib/udev
    _pick acp usr/share/alsa-card-profile

    _pick docs usr/share/doc

    _pick jack usr/bin/pw-jack
    _pick jack usr/include/jack
    _pick jack usr/lib/libjack*
    _pick jack usr/lib/pkgconfig/jack.pc
    _pick jack usr/share/man/man1/pw-jack.1
    _pick jack usr/share/pipewire/jack.conf

    _pick pulse usr/bin/pipewire-pulse
    _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-protocol-pulse.so
    _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-pulse-tunnel.so
    _pick pulse usr/lib/systemd/user/pipewire-pulse.*

    _pick gst usr/lib/gstreamer-1.0

    _pick zeroconf usr/lib/pipewire-$_ver/libpipewire-module-zeroconf-discover.so

    _pick v4l2 usr/bin/pw-v4l2 usr/lib/pipewire-$_ver/v4l2
  )

  mkdir -p "$pkgdir/etc/alsa/conf.d"
  ln -st "$pkgdir/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/50-pipewire.conf

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_alsa-card-profiles-fixed() {
  pkgdesc+=" - ALSA card profiles"
  license=(LGPL)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  mv acp/* "$pkgdir"
}

package_pipewire-docs-fixed() {
  pkgdesc+=" - documentation"
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  mv docs/* "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-alsa-fixed() {
  pkgdesc+=" - ALSA configuration"
  depends=(pipewire-session-manager)
  provides=(pulseaudio-alsa)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  mkdir -p "$pkgdir/etc/alsa/conf.d"
  ln -st "$pkgdir/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/99-pipewire-default.conf

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-alsa"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-jack-fixed() {
  pkgdesc+=" - JACK support"
  license+=(GPL2)  # libjackserver
  depends=(pipewire-session-manager libpipewire-$_ver.so)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  # TODO: drop jack provides and conflicts, this is a workaround for bad packaging and
  #       a pacman annoyance, it should be avoided
  conflicts+=('jack')
  provides+=('jack' 'libjack.so=0-64' 'libjacknet.so=0-64' 'libjackserver.so=0-64')

  mv jack/* "$pkgdir"

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-jack"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-pulse-fixed() {
  pkgdesc+=" - PulseAudio replacement"
  depends=(pipewire-session-manager libpipewire-$_ver.so libpulse.so
           libavahi-{client,common}.so)
  provides=(pulseaudio pulseaudio-bluetooth)
  conflicts=(pulseaudio pulseaudio-bluetooth)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})
  install=pipewire-pulse.install

  mv pulse/* "$pkgdir"

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-pulseaudio"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_gst-plugin-pipewire-fixed() {
  pkgdesc="Multimedia graph framework - pipewire plugin"
  depends=(pipewire-session-manager libpipewire-$_ver.so gst-plugins-base-libs)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  mv gst/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-zeroconf-fixed() {
  pkgdesc+=" - Zeroconf support"
  depends=(pipewire libpipewire-$_ver.so libavahi-{client,common}.so)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  mv zeroconf/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-v4l2-fixed() {
  pkgdesc+=" - V4L2 interceptor"
  depends=(pipewire-session-manager libpipewire-$_ver.so)
  conflicts+=(${pkgname%-$_pkgpostfix})
  provides+=(${pkgname%-$_pkgpostfix})

  mv v4l2/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

# vim:set sw=2 et:
