# Maintainer: tinywrkb <tinywrkb@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gnuplot-qt5
_pkgname=gnuplot
pkgver=5.2.7
pkgrel=1
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others"
arch=('x86_64')
url="http://www.gnuplot.info"
license=('custom')
depends=('readline' 'gd' 'cairo' 'libjpeg' 'lua' 'qt5-svg' 'gnutls' 'libcerf')
makedepends=('texinfo' 'texlive-core' 'emacs' 'texlive-latexextra' 'qt5-tools')
source=("https://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz"
        "lua53_compat.patch")
sha1sums=('fcd54ed0b1e5d7dda8151602b7198f0040462c70'
          '9005fa9e4da91ceedb8ccd1d761866e7b064f8b1')

prepare() {
  cd ${_pkgname}-${pkgver}

  # fix default source location; use the GDFONTPATH variable to modify at runtime
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c

  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c

  patch -p1 < "$srcdir"/lua53_compat.patch
}

build() {
  cd ${_pkgname}-${pkgver}

  MAKEINFO=/usr/bin/makeinfo  WX_CONFIG=/usr/bin/wx-config ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --with-bitmap-terminals \
              --disable-wxwidgets \
              --with-qt=qt5 \
              --with-texdir=/usr/share/texmf/tex/latex/gnuplot
  make pkglibexecdir=/usr/bin
}

package() {
  cd ${_pkgname}-${pkgver}
  make pkglibexecdir=/usr/bin DESTDIR="${pkgdir}" install

  install -Dm644 Copyright "${pkgdir}/usr/share/licenses/$_pkgname/Copyright"

  rm -f "${pkgdir}/usr/share/texmf-dist/ls-R"
}
